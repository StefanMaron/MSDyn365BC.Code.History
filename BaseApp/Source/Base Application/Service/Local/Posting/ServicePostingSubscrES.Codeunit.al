// ------------------------------------------------------------------------------------------------
// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License. See License.txt in the project root for license information.
// ------------------------------------------------------------------------------------------------
namespace Microsoft.Service.Posting;

using Microsoft.Bank.BankAccount;
using Microsoft.EServices.EDocument;
using Microsoft.Finance.Currency;
using Microsoft.Finance.GeneralLedger.Setup;
using Microsoft.Finance.ReceivablesPayables;
using Microsoft.Finance.VAT.Calculation;
using Microsoft.Foundation.AuditCodes;
using Microsoft.Foundation.PaymentTerms;
using Microsoft.Inventory.Intrastat;
using Microsoft.Sales.Customer;
using Microsoft.Sales.Receivables;
using Microsoft.Sales.Setup;
using Microsoft.Service.Document;
using Microsoft.Service.History;

codeunit 10789 "Service Posting Subscr. ES"
{
    var
#if not CLEAN27
        ServPostingJournalsMgt: Codeunit "Serv-Posting Journals Mgt.";
#endif
        CannotCreateCarteraDocErr: Label 'You do not have permissions to create Documents in Cartera.\Please, change the Payment Method.';
        Text1100000: Label 'The Credit Memo doesn''t have a Corrected Invoice No. Do you want to continue?';
        Text1100001: Label 'The posting process has been cancelled by the user.';
        Text1100002: Label 'Corrective Invoice';

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Service-Post", 'OnAfterPostWithLines', '', true, true)]
    local procedure OnAfterPostWithLines(var PassedServiceHeader: Record "Service Header"; var IsHandled: Boolean)
    var
        ServSIIManagement: Codeunit "Serv. SII Management";
    begin
        ServSIIManagement.OnAfterPostServiceDoc(PassedServiceHeader);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Service-Post", 'OnCheckAndSetConstantsOnBeforeSetPostingOptions', '', true, true)]
    local procedure OnCheckAndSetConstantsOnBeforeSetPostingOptions(var ServiceHeader: Record "Service Header"; Invoice: Boolean; Ship: Boolean)
    var
        TransportMethod: Record "Transport Method";
        PaymentTerms: Record "Payment Terms";
    begin
        if Invoice and (ServiceHeader."Document Type" <> ServiceHeader."Document Type"::"Credit Memo") then begin
            PaymentTerms.Get(ServiceHeader."Payment Terms Code");
            PaymentTerms.VerifyMaxNoDaysTillDueDate(ServiceHeader."Due Date", ServiceHeader."Document Date", ServiceHeader.FieldCaption(ServiceHeader."Due Date"));
        end;

        if TransportMethod.Get(ServiceHeader."Transport Method") and TransportMethod."Port/Airport" then
            ServiceHeader.TestField(ServiceHeader."Exit Point");

        if (Ship or Invoice) and (ServiceHeader."Document Type" <> ServiceHeader."Document Type"::"Credit Memo") then begin
            ServiceHeader.TestField(ServiceHeader."Payment Method Code");
            ServiceHeader.TestField(ServiceHeader."Payment Terms Code");
        end;
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Service-Post", 'OnValidatePostingAndDocumentDateOnAfterValidateDocumentDate', '', true, true)]
    local procedure OnValidatePostingAndDocumentDateOnAfterValidateDocumentDate(var ServiceHeader: Record "Service Header")
    begin
        ValidatePaymentTermsOnPost(ServiceHeader);
    end;

#if not CLEAN29
    procedure ValidatePaymentTermsOnPost(var ServiceHeader: Record "Service Header")
#else
    local procedure ValidatePaymentTermsOnPost(var ServiceHeader: Record "Service Header")
#endif
    var
        GeneralLedgerSetup: Record "General Ledger Setup";
        PaymentTerms: Record "Payment Terms";
        DueDateAdjust: Codeunit "Due Date-Adjust";
    begin
        GeneralLedgerSetup.GetRecordOnce();
        if (ServiceHeader."Document Type" <> ServiceHeader."Document Type"::"Credit Memo") or
           (GeneralLedgerSetup."Payment Discount Type" = GeneralLedgerSetup."Payment Discount Type"::"Calc. Pmt. Disc. on Lines")
        then
            if (ServiceHeader."Payment Terms Code" <> '') and (ServiceHeader."Document Date" <> 0D) then begin
                PaymentTerms.Get(ServiceHeader."Payment Terms Code");
                ServiceHeader."Due Date" := CalcDate(PaymentTerms."Due Date Calculation", ServiceHeader."Document Date");
                DueDateAdjust.SalesAdjustDueDate(
                  ServiceHeader."Due Date", ServiceHeader."Document Date", PaymentTerms.CalculateMaxDueDate(ServiceHeader."Document Date"), ServiceHeader."Bill-to Customer No.");
                ServiceHeader."Pmt. Discount Date" := CalcDate(PaymentTerms."Discount Date Calculation", ServiceHeader."Document Date");
            end else begin
                ServiceHeader."Due Date" := ServiceHeader."Document Date";
                DueDateAdjust.SalesAdjustDueDate(ServiceHeader."Due Date", ServiceHeader."Document Date", 99991231D, ServiceHeader."Bill-to Customer No.");
                ServiceHeader."Pmt. Discount Date" := ServiceHeader."Document Date";
            end;
        if (ServiceHeader."Document Type" = ServiceHeader."Document Type"::"Credit Memo") and (ServiceHeader."Payment Terms Code" <> '') then begin
            PaymentTerms.Get(ServiceHeader."Payment Terms Code");
            if not PaymentTerms."Calc. Pmt. Disc. on Cr. Memos" then begin
                ServiceHeader."Due Date" := ServiceHeader."Document Date";
                ServiceHeader."Payment Discount %" := 0;
                ServiceHeader."Pmt. Discount Date" := 0D;
            end;
        end;
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Documents Mgt.", 'OnPostDocumentLinesOnAfterPostSalesAndVAT', '', true, true)]
    local procedure OnPostDocumentLinesOnAfterPostSalesAndVAT(var ServiceHeader: Record "Service Header"; var TotalServiceLine: Record "Service Line"; var Window: Dialog; GenJnlLineDocNo: Code[20]; GenJnlLineExtDocNo: Text[35]; Invoice: Boolean)
    begin
        CreateBills(ServiceHeader, TotalServiceLine, Window, GenJnlLineDocNo, GenJnlLineExtDocNo, Invoice);
    end;

    local procedure CreateBills(ServiceHeader: Record "Service Header"; var TotalServiceLine: Record "Service Line"; var Window: Dialog; GenJnlLineDocNo: Code[20]; GenJnlLineExtDocNo: Code[35]; Invoice: Boolean)
    var
        CustLedgEntry: Record "Cust. Ledger Entry";
        PaymentMethod: Record "Payment Method";
        CarteraSetup: Record "Cartera Setup";
        SourceCodeSetup: Record "Source Code Setup";
        ServInvoiceSplitPayment: Codeunit "Serv. Invoice-Split Payment";
    begin
        CustLedgEntry.Find('+');
        if PaymentMethod.Get(ServiceHeader."Payment Method Code") then
            if (PaymentMethod."Create Bills" or PaymentMethod."Invoices to Cartera") and
               (not CarteraSetup.ReadPermission) and Invoice
            then
                Error(CannotCreateCarteraDocErr);
        OnCreateBillsOnBeforeSplitServiceInv(ServiceHeader, CustLedgEntry, TotalServiceLine);
#if not CLEAN27
        ServPostingJournalsMgt.RunOnCreateBillsOnBeforeSplitServiceInv(ServiceHeader, CustLedgEntry, TotalServiceLine);
#endif
        SourceCodeSetup.Get();
        if (ServiceHeader."Bal. Account No." = '') and (ServiceHeader."Document Type" <> ServiceHeader."Document Type"::"Credit Memo") and CarteraSetup.ReadPermission then
            ServInvoiceSplitPayment.SplitServiceInvoice(
              ServiceHeader, CustLedgEntry, Window, SourceCodeSetup."Service Management", GenJnlLineExtDocNo, GenJnlLineDocNo,
              -(TotalServiceLine."Amount Including VAT" - TotalServiceLine.Amount));
    end;

    [IntegrationEvent(false, false)]
    local procedure OnCreateBillsOnBeforeSplitServiceInv(ServiceHeader: Record "Service Header"; var CustLedgerEntry: Record "Cust. Ledger Entry"; var TotalServiceLine: Record "Service Line")
    begin
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Documents Mgt.", 'OnAfterGetAndCheckCustomer', '', true, true)]
    local procedure OnAfterGetAndCheckCustomer(var ServiceHeader: Record "Service Header")
    begin
        TestSalesEfects(ServiceHeader);
    end;

    local procedure TestSalesEfects(ServiceHeader: Record "Service Header")
    var
        CustLedgEntry: Record "Cust. Ledger Entry";
        Customer: Record Customer;
        ShowError: Boolean;
        Text1100000: Label 'At least one document of %1 No. %2 is closed or in a Bill Group.';
        Text1100001: Label 'This will avoid the document to be settled.\';
        Text1100002: Label 'The posting process of %3 No. %4 will not settle any document.\';
        ApplyToOldestErr: Label 'Due this customer is using Apply to Oldest Application Method, please remove the lines for the Bill Group before posting.';
        ManualApplicationErr: Label 'Please change the Applies-to Doc. No. or remove the applied document from the Bill Group before posting.';
    begin
        if ServiceHeader."Document Type" <> ServiceHeader."Document Type"::"Credit Memo" then
            exit;

        // Without a customer the posting cannot succeed, so there is nothing to settle and the check is skipped.
        if not Customer.Get(ServiceHeader."Bill-to Customer No.") then
            exit;

        CustLedgEntry.SetCurrentKey("Document No.", "Document Type", "Customer No.");
        CustLedgEntry.SetFilter("Document Type", '%1|%2', CustLedgEntry."Document Type"::Invoice,
          CustLedgEntry."Document Type"::Bill);
        CustLedgEntry.SetFilter("Document Situation", '<>%1', CustLedgEntry."Document Situation"::" ");
        CustLedgEntry.SetRange("Customer No.", ServiceHeader."Bill-to Customer No.");
        CustLedgEntry.SetRange(Open, true);

        if Customer."Application Method" = Customer."Application Method"::Manual then begin
            if ServiceHeader."Applies-to Doc. No." = '' then
                exit;
            CustLedgEntry.SetRange("Document Type", ServiceHeader."Applies-to Doc. Type");
            CustLedgEntry.SetRange("Document No.", ServiceHeader."Applies-to Doc. No.");
        end;

        ShowError := false;
        if CustLedgEntry.Find('-') then
            repeat
                if CustLedgEntry."Document Situation" <> CustLedgEntry."Document Situation"::Cartera then
                    if not ((CustLedgEntry."Document Situation" in
                             [CustLedgEntry."Document Situation"::"Closed Documents",
                              CustLedgEntry."Document Situation"::"Closed BG/PO"]) and
                            (CustLedgEntry."Document Status" = CustLedgEntry."Document Status"::Rejected))
                    then
                        ShowError := true;
            until CustLedgEntry.Next() = 0;

        if ShowError then
            if Customer."Application Method" = Customer."Application Method"::Manual then
                Error(
                  Text1100000 + Text1100001 + Text1100002 + ManualApplicationErr,
                  Format(CustLedgEntry."Document Type"),
                  Format(CustLedgEntry."Document No."),
                  Format(ServiceHeader."Document Type"),
                  Format(ServiceHeader."No."))
            else
                Error(
                  Text1100000 + Text1100001 + Text1100002 + ApplyToOldestErr,
                  Format(CustLedgEntry."Document Type"),
                  Format(CustLedgEntry."Document No."),
                  Format(ServiceHeader."Document Type"),
                  Format(ServiceHeader."No."));
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Documents Mgt.", 'OnFinalizeInvoiceDocumentOnBeforeServiceInvoiceHeaderInsert', '', true, true)]
    local procedure OnFinalizeInvoiceDocumentOnBeforeServiceInvoiceHeaderInsert(var ServiceInvoiceHeaderToInsert: Record "Service Invoice Header"; var TempServiceInvoiceHeader: Record "Service Invoice Header" temporary; var TempServiceHeader: Record "Service Header" temporary)
    begin
        ServiceInvoiceHeaderToInsert.SetSIIFirstSummaryDocNo(ServiceInvoiceHeaderToInsert.GetSIIFirstSummaryDocNo());
        ServiceInvoiceHeaderToInsert.SetSIILastSummaryDocNo(ServiceInvoiceHeaderToInsert.GetSIILastSummaryDocNo());
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Documents Mgt.", 'OnFinalizeCrMemoDocumentOnBeforeServiceCreditMemoHeaderInsert', '', true, true)]
    local procedure OnFinalizeCrMemoDocumentOnBeforeServiceCreditMemoHeaderInsert(var ServiceCrMemoHeaderToInsert: Record "Service Cr.Memo Header"; var TempServiceCrMemoHeader: Record "Service Cr.Memo Header" temporary; var TempServiceHeader: Record "Service Header" temporary)
    begin
        ServiceCrMemoHeaderToInsert.SetSIIFirstSummaryDocNo(ServiceCrMemoHeaderToInsert.GetSIIFirstSummaryDocNo());
        ServiceCrMemoHeaderToInsert.SetSIILastSummaryDocNo(ServiceCrMemoHeaderToInsert.GetSIILastSummaryDocNo());
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Documents Mgt.", 'OnGetAndCheckCustomerOnAfterCheckBlocked', '', true, true)]
    local procedure OnGetAndCheckCustomerOnAfterCheckBlocked(var ServiceHeader: Record "Service Header")
    var
        SalesSetup: Record "Sales & Receivables Setup";
    begin
        if ServiceHeader."Document Type" = ServiceHeader."Document Type"::"Credit Memo" then begin
            SalesSetup.Get();
            if SalesSetup."Correct. Doc. No. Mandatory" then
                ServiceHeader.TestField(ServiceHeader."Corrected Invoice No.")
            else
                if ServiceHeader."Corrected Invoice No." = '' then
                    if not Confirm(Text1100000, false) then
                        Error(Text1100001);
            if (ServiceHeader."Corrected Invoice No." <> '') and (ServiceHeader."Posting Description" = '') then
                ServiceHeader."Posting Description" := Format(Text1100002) + ' ' + ServiceHeader."No."
        end;
    end;

    [EventSubscriber(ObjectType::Table, Database::"Service Header", 'OnBeforeValidatePaymentTerms', '', true, true)]
    local procedure OnBeforeValidatePaymentTerms(var ServiceHeader: Record "Service Header"; var IsHandled: Boolean)
    var
        PaymentTerms: Record "Payment Terms";
        GeneralLedgerSetup: Record "General Ledger Setup";
        AdjustDueDate: Codeunit "Due Date-Adjust";
    begin
        GeneralLedgerSetup.GetRecordOnce();
        if (ServiceHeader."Document Type" <> ServiceHeader."Document Type"::"Credit Memo") or
           (GeneralLedgerSetup."Payment Discount Type" = GeneralLedgerSetup."Payment Discount Type"::"Calc. Pmt. Disc. on Lines")
        then
            if (ServiceHeader."Payment Terms Code" <> '') and (ServiceHeader."Document Date" <> 0D) then begin
                PaymentTerms.Get(ServiceHeader."Payment Terms Code");
                ServiceHeader."Due Date" := CalcDate(PaymentTerms."Due Date Calculation", ServiceHeader."Document Date");
                AdjustDueDate.SalesAdjustDueDate(
                  ServiceHeader."Due Date", ServiceHeader."Document Date", PaymentTerms.CalculateMaxDueDate(ServiceHeader."Document Date"), ServiceHeader."Bill-to Customer No.");
                ServiceHeader."Pmt. Discount Date" := CalcDate(PaymentTerms."Discount Date Calculation", ServiceHeader."Document Date");
                ServiceHeader.Validate("Payment Discount %", PaymentTerms."Discount %");
            end else begin
                ServiceHeader."Due Date" := ServiceHeader."Document Date";
                AdjustDueDate.SalesAdjustDueDate(ServiceHeader."Due Date", ServiceHeader."Document Date", 99991231D, ServiceHeader."Bill-to Customer No.");
                IsHandled := false;
                ServiceHeader.RunOnValidatePaymentTermsCodeOnBeforeCalcPmtDiscDate(ServiceHeader, IsHandled);
                if not IsHandled then
                    ServiceHeader."Pmt. Discount Date" := ServiceHeader."Document Date";
                ServiceHeader.Validate("Payment Discount %", 0);
            end
        else
            if ServiceHeader."Payment Terms Code" <> '' then begin
                PaymentTerms.Get(ServiceHeader."Payment Terms Code");
                ServiceHeader.Validate("Payment Discount %", PaymentTerms."Discount %");
            end else
                ServiceHeader.Validate("Payment Discount %", 0);

        if (ServiceHeader."Document Type" = ServiceHeader."Document Type"::"Credit Memo") and
           not PaymentTerms."Calc. Pmt. Disc. on Cr. Memos"
        then begin
            IsHandled := false;
            ServiceHeader.RunOnValidatePaymentTermsCodeOnBeforeValidateDueDate(ServiceHeader, IsHandled);
            if not IsHandled then
                ServiceHeader.Validate("Due Date", ServiceHeader."Document Date");
            ServiceHeader.Validate("Pmt. Discount Date", 0D);
            ServiceHeader.Validate("Payment Discount %", 0);
        end;
        IsHandled := true;
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Amounts Mgt.", 'OnSumServiceLine2OnAfterSetVATPercentFromServLine', '', true, true)]
    local procedure OnSumServiceLine2OnAfterSetVATPercentFromServLine(var TotalServiceLine: Record "Service Line"; var ServiceLine: Record "Service Line")
    begin
        TotalServiceLine."EC %" := ServiceLine."EC %";
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Amounts Mgt.", 'OnDivideAmountOnSetVATPercentFromVATAmountLine', '', true, true)]
    local procedure OnDivideAmountOnSetVATPercentFromVATAmountLine(var ServiceLine: Record "Service Line"; var TempVATAmountLine: Record "VAT Amount Line")
    begin
        ServiceLine."EC %" := TempVATAmountLine."EC %";
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Amounts Mgt.", 'OnDivideAmountOnAfterCalcRemainderVATAmount', '', true, true)]
    local procedure OnDivideAmountOnAfterCalcRemainderVATAmount(var TempVATAmountLineRemainder: Record "VAT Amount Line" temporary; var ServiceLine: Record "Service Line"; var TempVATAmountLine: Record "VAT Amount Line" temporary)
    begin
        TempVATAmountLineRemainder."EC Amount" +=
            TempVATAmountLine."EC Amount" *
            (ServiceLine.CalcLineAmount() - ServiceLine."Pmt. Discount Amount") /
            (TempVATAmountLine.CalcLineAmount() - TempVATAmountLine."Pmt. Discount Amount");
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Amounts Mgt.", 'OnDivideAmountOnAfterCalcServiceLineAmountIncludingVAT', '', true, true)]
    local procedure OnDivideAmountOnAfterCalcServiceLineAmountIncludingVAT(var ServiceLine: Record "Service Line"; var TempVATAmountLineRemainder: Record "VAT Amount Line" temporary; var Currency: Record "Currency")
    begin
        ServiceLine."Amount Including VAT" += Round(TempVATAmountLineRemainder."EC Amount", Currency."Amount Rounding Precision");
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Amounts Mgt.", 'OnDivideAmountOnAfterSetVATAmountLineRemainderToZero', '', true, true)]
    local procedure OnDivideAmountOnAfterSetVATAmountLineRemainderToZero(var TempVATAmountLineRemainder: Record "VAT Amount Line" temporary)
    begin
        TempVATAmountLineRemainder."EC Amount" := 0;
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Serv-Amounts Mgt.", 'OnDivideAmountOnAfterCalcVATAmountLineRemainder', '', true, true)]
    local procedure OnDivideAmountOnAfterCalcVATAmountLineRemainder(var TempVATAmountLineRemainder: Record "VAT Amount Line" temporary; var TempVATAmountLine: Record "VAT Amount Line" temporary; var ServiceLine: Record "Service Line")
    begin
        TempVATAmountLineRemainder."EC Amount" +=
            TempVATAmountLine."EC Amount" *
            (ServiceLine.CalcLineAmount() - ServiceLine."Pmt. Discount Amount") /
            (TempVATAmountLine.CalcLineAmount() - TempVATAmountLine."Pmt. Discount Amount");
    end;
}