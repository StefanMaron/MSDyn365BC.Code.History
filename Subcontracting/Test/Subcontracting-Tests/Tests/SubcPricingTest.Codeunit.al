// ------------------------------------------------------------------------------------------------
// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License. See License.txt in the project root for license information.
// ------------------------------------------------------------------------------------------------
namespace Microsoft.Manufacturing.Subcontracting.Test;

using Microsoft.Foundation.Enums;
using Microsoft.Foundation.UOM;
using Microsoft.Inventory.Item;
using Microsoft.Inventory.Requisition;
using Microsoft.Manufacturing.Capacity;
using Microsoft.Manufacturing.Document;
using Microsoft.Manufacturing.Reports;
using Microsoft.Manufacturing.Routing;
using Microsoft.Manufacturing.Subcontracting;
using Microsoft.Manufacturing.WorkCenter;
using Microsoft.Purchases.Document;
using Microsoft.Purchases.Vendor;
using System.TestLibraries.Utilities;

codeunit 139982 "Subc. Pricing Test"
{
    // [FEATURE] Subcontracting Pricing
    Subtype = Test;
    TestPermissions = Disabled;
    TestType = IntegrationTest;

    trigger OnRun()
    begin
        IsInitialized := false;
    end;

    [Test]
    procedure RoutingPriceUsesOrderUoMWhenMultipleUoMPricesExist()
    var
        Item: Record Item;
        ItemUOM: Record "Item Unit of Measure";
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        InSubcontractorPrice: Record "Subcontractor Price";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        UnitCostCalcType: Enum "Unit Cost Calculation Type";
        AltUOMCode: Code[10];
        DirUnitCost, IndirCostPct, OvhdRate, UnitCost : Decimal;
        PcsPrice, SetPrice : Decimal;
        QtyPerSet: Integer;
    begin
        // [SCENARIO 636059] SetRoutingPriceListCost must select the Subcontractor Price row matching
        // the order's Unit of Measure (with blank fallback). With prices in both Base UoM and an
        // alternative UoM that sorts after it, the routing line must pick the Base UoM price when
        // the order is in Base UoM — not the alphabetically-last alternative-UoM row.
        Initialize();

        // [GIVEN] Item with Base UoM and an alternative UoM (10 base per alt) whose code sorts after the base.
        LibraryInventory.CreateItem(Item);
        QtyPerSet := 10;
        AltUOMCode := CreateUOMCodeSortingAfter(Item."Base Unit of Measure");
        LibraryInventory.CreateItemUnitOfMeasure(ItemUOM, Item."No.", AltUOMCode, QtyPerSet);

        // [GIVEN] Vendor and Work Center with the vendor as its subcontractor; zero indirect/overhead.
        LibraryPurchase.CreateVendor(Vendor);
        LibraryManufacturing.CreateWorkCenter(WorkCenter);
        WorkCenter.Validate("Subcontractor No.", Vendor."No.");
        WorkCenter.Validate("Indirect Cost %", 0);
        WorkCenter.Validate("Overhead Rate", 0);
        WorkCenter.Modify(true);

        // [GIVEN] Two subcontractor prices — Base UoM = 1001, alternative UoM = 1004.
        PcsPrice := 1001;
        SetPrice := 1004;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", PcsPrice);
        SubcontractorPrice.Modify(true);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), AltUOMCode, 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", SetPrice);
        SubcontractorPrice.Modify(true);

        // [GIVEN] InSubcontractorPrice staged as SetSubcontractorPriceForPriceCalculation would — order in Base UoM.
        InSubcontractorPrice."Vendor No." := Vendor."No.";
        InSubcontractorPrice."Item No." := Item."No.";
        InSubcontractorPrice."Standard Task Code" := '';
        InSubcontractorPrice."Work Center No." := WorkCenter."No.";
        InSubcontractorPrice."Variant Code" := '';
        InSubcontractorPrice."Unit of Measure Code" := Item."Base Unit of Measure";
        InSubcontractorPrice."Starting Date" := WorkDate();
        InSubcontractorPrice."Currency Code" := '';

        // [WHEN] SetRoutingPriceListCost runs for a Prod. Order Routing Line of qty 1 in the Base UoM.
        SubcPriceManagement.SetRoutingPriceListCost(
            InSubcontractorPrice, WorkCenter, DirUnitCost, IndirCostPct, OvhdRate, UnitCost, UnitCostCalcType, 1, 1, 1);

        // [THEN] Direct Unit Cost equals the Base UoM price (1001), not the alt-UoM derived 100.40.
        Assert.AreEqual(
            PcsPrice, DirUnitCost,
            'SetRoutingPriceListCost must pick the Subcontractor Price row matching the order''s Unit of Measure.');
    end;

    [Test]
    procedure RoutingPriceUsesLCYWhenForeignCurrencyPriceExists()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        InSubcontractorPrice: Record "Subcontractor Price";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        UnitCostCalcType: Enum "Unit Cost Calculation Type";
        ForeignCurrencyCode: Code[10];
        DirUnitCost, IndirCostPct, OvhdRate, UnitCost : Decimal;
        LCYPrice: Decimal;
    begin
        // [SCENARIO 638367] SetRoutingPriceListCost must filter Subcontractor Price by Currency Code so the
        // LCY (blank-currency) price drives Calc Standard Cost / Prod. Order Routing, not the alphabetically-last
        // foreign-currency row picked by FindLast() when Currency Code is left unfiltered.
        Initialize();

        // [GIVEN] Item, vendor and a subcontracting work center with zero indirect/overhead.
        CreateItemVendorAndSubcontractingWorkCenter(Item, Vendor, WorkCenter);

        // [GIVEN] A foreign currency whose code sorts after the blank LCY code.
        ForeignCurrencyCode := LibraryERM.CreateCurrencyWithExchangeRate(WorkDate(), 15, 15);

        // [GIVEN] Two subcontractor prices — LCY = 10 and the foreign currency = 20.
        LCYPrice := 10;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", LCYPrice);
        SubcontractorPrice.Modify(true);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, ForeignCurrencyCode);
        SubcontractorPrice.Validate("Direct Unit Cost", 20);
        SubcontractorPrice.Modify(true);

        // [GIVEN] InSubcontractorPrice staged for an LCY routing line (blank Currency Code).
        StageInSubcontractorPrice(InSubcontractorPrice, Vendor, WorkCenter, Item, '', '');

        // [WHEN] SetRoutingPriceListCost runs for a routing line of qty 1 in the base UoM.
        SubcPriceManagement.SetRoutingPriceListCost(
            InSubcontractorPrice, WorkCenter, DirUnitCost, IndirCostPct, OvhdRate, UnitCost, UnitCostCalcType, 1, 1, 1);

        // [THEN] The LCY price (10) is used, not the foreign-currency price converted to LCY.
        Assert.AreEqual(
            LCYPrice, DirUnitCost,
            'SetRoutingPriceListCost must use the LCY subcontractor price, not a foreign-currency row.');
    end;

    [Test]
    procedure RoutingPriceFallsBackToCatchAllStandardTaskCode()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        InSubcontractorPrice: Record "Subcontractor Price";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        UnitCostCalcType: Enum "Unit Cost Calculation Type";
        DirUnitCost, IndirCostPct, OvhdRate, UnitCost : Decimal;
        CatchAllPrice: Decimal;
    begin
        // [SCENARIO 638400] On the Prod. Order Routing path, SetRoutingPriceListCost must fall back to the
        // catch-all (blank Standard Task Code) subcontractor price when the routing line carries a Standard
        // Task Code that has no dedicated price — instead of leaving the routing cost in place.
        Initialize();

        // [GIVEN] Item, vendor, subcontracting work center and a single catch-all price (blank task, blank variant).
        CreateItemVendorAndSubcontractingWorkCenter(Item, Vendor, WorkCenter);
        CatchAllPrice := 333;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", CatchAllPrice);
        SubcontractorPrice.Modify(true);

        // [GIVEN] InSubcontractorPrice staged for a routing line with a Standard Task Code that has no own price.
        StageInSubcontractorPrice(InSubcontractorPrice, Vendor, WorkCenter, Item, '', 'TASK1');

        // [WHEN] SetRoutingPriceListCost runs.
        SubcPriceManagement.SetRoutingPriceListCost(
            InSubcontractorPrice, WorkCenter, DirUnitCost, IndirCostPct, OvhdRate, UnitCost, UnitCostCalcType, 1, 1, 1);

        // [THEN] The catch-all price (333) is applied.
        Assert.AreEqual(
            CatchAllPrice, DirUnitCost,
            'SetRoutingPriceListCost must fall back to the blank-Standard-Task-Code price.');
    end;

    [Test]
    procedure RoutingPriceFallsBackToCatchAllVariantCode()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        InSubcontractorPrice: Record "Subcontractor Price";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        UnitCostCalcType: Enum "Unit Cost Calculation Type";
        DirUnitCost, IndirCostPct, OvhdRate, UnitCost : Decimal;
        CatchAllPrice: Decimal;
    begin
        // [SCENARIO 638400] On the Prod. Order Routing path, SetRoutingPriceListCost must fall back to the
        // catch-all (blank Variant Code) subcontractor price when the prod. order line has a Variant Code
        // that has no dedicated price.
        Initialize();

        // [GIVEN] Item, vendor, subcontracting work center and a single catch-all price (blank variant, blank task).
        CreateItemVendorAndSubcontractingWorkCenter(Item, Vendor, WorkCenter);
        CatchAllPrice := 333;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", CatchAllPrice);
        SubcontractorPrice.Modify(true);

        // [GIVEN] InSubcontractorPrice staged for a routing line with a Variant Code that has no own price.
        StageInSubcontractorPrice(InSubcontractorPrice, Vendor, WorkCenter, Item, 'VAR1', '');

        // [WHEN] SetRoutingPriceListCost runs.
        SubcPriceManagement.SetRoutingPriceListCost(
            InSubcontractorPrice, WorkCenter, DirUnitCost, IndirCostPct, OvhdRate, UnitCost, UnitCostCalcType, 1, 1, 1);

        // [THEN] The catch-all price (333) is applied.
        Assert.AreEqual(
            CatchAllPrice, DirUnitCost,
            'SetRoutingPriceListCost must fall back to the blank-Variant-Code price.');
    end;

    [Test]
    procedure RoutingPricePrefersVariantSpecificOverStandardTaskSpecific()
    var
        Item: Record Item;
        ItemVariant: Record "Item Variant";
        StandardTask: Record "Standard Task";
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        InSubcontractorPrice: Record "Subcontractor Price";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        UnitCostCalcType: Enum "Unit Cost Calculation Type";
        DirUnitCost, IndirCostPct, OvhdRate, UnitCost : Decimal;
        VariantPrice, TaskPrice : Decimal;
    begin
        // [SCENARIO 638400] When a routing line matches BOTH a variant-specific price (blank Standard Task Code)
        // and a standard-task-specific price (blank Variant Code), the lookup must be deterministic. With empty
        // fallback on both fields, FindLast follows the Subcontractor Price primary key order, where Variant Code
        // (field 4) precedes Standard Task Code (field 5), so the variant-specific row wins. This pins that
        // precedence so it stays consistent across the routing, worksheet, and purchase-line lookups.
        Initialize();

        // [GIVEN] Item (with a variant), a standard task, vendor and a subcontracting work center.
        CreateItemVendorAndSubcontractingWorkCenter(Item, Vendor, WorkCenter);
        LibraryInventory.CreateItemVariant(ItemVariant, Item."No.");
        LibraryManufacturing.CreateStandardTask(StandardTask);

        // [GIVEN] A variant-specific price (blank task = 100) and a standard-task-specific price (blank variant = 200).
        VariantPrice := 100;
        TaskPrice := 200;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', ItemVariant.Code, WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", VariantPrice);
        SubcontractorPrice.Modify(true);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", StandardTask.Code, '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", TaskPrice);
        SubcontractorPrice.Modify(true);

        // [GIVEN] InSubcontractorPrice staged for a routing line carrying both the variant and the standard task.
        StageInSubcontractorPrice(InSubcontractorPrice, Vendor, WorkCenter, Item, ItemVariant.Code, StandardTask.Code);

        // [WHEN] SetRoutingPriceListCost runs.
        SubcPriceManagement.SetRoutingPriceListCost(
            InSubcontractorPrice, WorkCenter, DirUnitCost, IndirCostPct, OvhdRate, UnitCost, UnitCostCalcType, 1, 1, 1);

        // [THEN] The variant-specific price (100) wins over the standard-task-specific price (200).
        Assert.AreEqual(
            VariantPrice, DirUnitCost,
            'When both a variant-specific and a standard-task-specific price match, the variant-specific price (per PK order) must win.');
    end;

    [Test]
    procedure WorksheetPriceFallsBackToCatchAllStandardTaskCode()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        RequisitionLine: Record "Requisition Line";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        CatchAllPrice: Decimal;
    begin
        // [SCENARIO 638400] On the Subcontracting Worksheet path, GetSubcPriceForReqLine must fall back to the
        // catch-all (blank Standard Task Code) subcontractor price when the worksheet line carries a Standard
        // Task Code that has no dedicated price.
        Initialize();

        // [GIVEN] Item, vendor, subcontracting work center and a single catch-all price (blank task, blank variant).
        CreateItemVendorAndSubcontractingWorkCenter(Item, Vendor, WorkCenter);
        CatchAllPrice := 333;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", CatchAllPrice);
        SubcontractorPrice.Modify(true);

        // [GIVEN] A worksheet (requisition) line with a Standard Task Code that has no own price.
        StageRequisitionLine(RequisitionLine, Vendor, WorkCenter, Item, '', 'TASK1');

        // [WHEN] GetSubcPriceForReqLine runs.
        SubcPriceManagement.GetSubcPriceForReqLine(RequisitionLine, '');

        // [THEN] The catch-all price (333) is applied to the worksheet line.
        Assert.AreEqual(
            CatchAllPrice, RequisitionLine."Direct Unit Cost",
            'GetSubcPriceForReqLine must fall back to the blank-Standard-Task-Code price.');
    end;

    [Test]
    procedure WorksheetPriceFallsBackToCatchAllVariantCode()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        RequisitionLine: Record "Requisition Line";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        CatchAllPrice: Decimal;
    begin
        // [SCENARIO 638400] On the Subcontracting Worksheet path, GetSubcPriceForReqLine must fall back to the
        // catch-all (blank Variant Code) subcontractor price when the worksheet line has a Variant Code that
        // has no dedicated price.
        Initialize();

        // [GIVEN] Item, vendor, subcontracting work center and a single catch-all price (blank variant, blank task).
        CreateItemVendorAndSubcontractingWorkCenter(Item, Vendor, WorkCenter);
        CatchAllPrice := 333;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", CatchAllPrice);
        SubcontractorPrice.Modify(true);

        // [GIVEN] A worksheet (requisition) line with a Variant Code that has no own price.
        StageRequisitionLine(RequisitionLine, Vendor, WorkCenter, Item, 'VAR1', '');

        // [WHEN] GetSubcPriceForReqLine runs.
        SubcPriceManagement.GetSubcPriceForReqLine(RequisitionLine, '');

        // [THEN] The catch-all price (333) is applied to the worksheet line.
        Assert.AreEqual(
            CatchAllPrice, RequisitionLine."Direct Unit Cost",
            'GetSubcPriceForReqLine must fall back to the blank-Variant-Code price.');
    end;

    [Test]
    procedure ProdOrderRoutingUnitCostUsesLCYWhenForeignCurrencyPriceExists()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        ForeignCurrencyCode: Code[10];
        LCYPrice: Decimal;
    begin
        // [SCENARIO 638367] Refreshing a Released Production Order must price the subcontracting Prod. Order
        // Routing line from the LCY (blank-currency) subcontractor price, not from the alphabetically-last
        // foreign-currency price converted to LCY. End-to-end check of the routing path via the
        // OnAfterTransferRoutingLine subscriber -> ApplySubcontractorPricingToProdOrderRouting.
        Initialize();

        // [GIVEN] A subcontracting item with a single-operation routing on a subcontracting work center.
        CreateSubcontractingItemWithSingleOperationRouting(Item, Vendor, WorkCenter, '');

        // [GIVEN] A foreign currency with a non-LCY exchange rate whose code sorts after blank/LCY.
        ForeignCurrencyCode := LibraryERM.CreateCurrencyWithExchangeRate(WorkDate(), 15, 15);

        // [GIVEN] Two subcontractor prices for the item/work center/vendor — LCY = 10 and foreign = 20.
        LCYPrice := 10;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", LCYPrice);
        SubcontractorPrice.Modify(true);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, ForeignCurrencyCode);
        SubcontractorPrice.Validate("Direct Unit Cost", 20);
        SubcontractorPrice.Modify(true);

        // [WHEN] A Released Production Order for the item is created and refreshed.
        SubcontractingMgmtLibrary.CreateAndRefreshProductionOrder(
            ProductionOrder, "Production Order Status"::Released, "Prod. Order Source Type"::Item, Item."No.", 1);

        // [THEN] The Prod. Order Routing line Direct Unit Cost and Unit Cost per equal the LCY price (10).
        ProdOrderRoutingLine.SetRange("Prod. Order No.", ProductionOrder."No.");
        ProdOrderRoutingLine.FindFirst();
        Assert.AreEqual(
            LCYPrice, ProdOrderRoutingLine."Direct Unit Cost",
            'Prod. Order Routing Direct Unit Cost must use the LCY subcontractor price, not a foreign-currency one.');
        Assert.AreEqual(
            LCYPrice, ProdOrderRoutingLine."Unit Cost per",
            'Prod. Order Routing Unit Cost per must use the LCY subcontractor price, not a foreign-currency one.');
    end;

    [Test]
    procedure ProdOrderRoutingUnitCostUsesLCYAmongMultipleForeignCurrencies()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        FirstForeignCurrencyCode: Code[10];
        SecondForeignCurrencyCode: Code[10];
        LCYPrice: Decimal;
    begin
        // [SCENARIO 638367] Even when several foreign-currency prices exist (all sorting after blank on the
        // primary key), the Prod. Order Routing line must still resolve to the single LCY (blank-currency) price.
        Initialize();

        // [GIVEN] A subcontracting item with a single-operation routing on a subcontracting work center.
        CreateSubcontractingItemWithSingleOperationRouting(Item, Vendor, WorkCenter, '');

        // [GIVEN] Two foreign currencies with non-LCY exchange rates.
        FirstForeignCurrencyCode := LibraryERM.CreateCurrencyWithExchangeRate(WorkDate(), 15, 15);
        SecondForeignCurrencyCode := LibraryERM.CreateCurrencyWithExchangeRate(WorkDate(), 12, 12);

        // [GIVEN] An LCY price (10) and two foreign-currency prices (20 and 18).
        LCYPrice := 10;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", LCYPrice);
        SubcontractorPrice.Modify(true);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, FirstForeignCurrencyCode);
        SubcontractorPrice.Validate("Direct Unit Cost", 20);
        SubcontractorPrice.Modify(true);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, SecondForeignCurrencyCode);
        SubcontractorPrice.Validate("Direct Unit Cost", 18);
        SubcontractorPrice.Modify(true);

        // [WHEN] A Released Production Order for the item is created and refreshed.
        SubcontractingMgmtLibrary.CreateAndRefreshProductionOrder(
            ProductionOrder, "Production Order Status"::Released, "Prod. Order Source Type"::Item, Item."No.", 1);

        // [THEN] The Prod. Order Routing line still resolves to the LCY price (10).
        ProdOrderRoutingLine.SetRange("Prod. Order No.", ProductionOrder."No.");
        ProdOrderRoutingLine.FindFirst();
        Assert.AreEqual(
            LCYPrice, ProdOrderRoutingLine."Direct Unit Cost",
            'Prod. Order Routing Direct Unit Cost must use the LCY price even among multiple foreign-currency prices.');
        Assert.AreEqual(
            LCYPrice, ProdOrderRoutingLine."Unit Cost per",
            'Prod. Order Routing Unit Cost per must use the LCY price even among multiple foreign-currency prices.');
    end;

    [Test]
    procedure ProdOrderRoutingUnitCostUsesVendorCurrencyPriceWhenVendorHasCurrency()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        VendorCurrencyCode: Code[10];
        LCYPrice: Decimal;
        VendorCurrencyPrice: Decimal;
    begin
        // [SCENARIO 638367] When the subcontractor vendor has a foreign Currency Code and a Subcontractor Price
        // exists in that currency, the Prod. Order Routing line must be priced from the vendor-currency price
        // (converted to LCY) so it stays consistent with the purchase order, not from the blank/LCY price.
        Initialize();

        // [GIVEN] A subcontractor vendor with a foreign currency (1:1 exchange rate) and a subcontracting item.
        VendorCurrencyCode := LibraryERM.CreateCurrencyWithExchangeRate(WorkDate(), 1, 1);
        CreateSubcontractingItemWithSingleOperationRouting(Item, Vendor, WorkCenter, VendorCurrencyCode);

        // [GIVEN] Two subcontractor prices — LCY = 10 (blank currency) and vendor-currency = 20.
        LCYPrice := 10;
        VendorCurrencyPrice := 20;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", LCYPrice);
        SubcontractorPrice.Modify(true);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, VendorCurrencyCode);
        SubcontractorPrice.Validate("Direct Unit Cost", VendorCurrencyPrice);
        SubcontractorPrice.Modify(true);

        // [WHEN] A Released Production Order for the item is created and refreshed.
        SubcontractingMgmtLibrary.CreateAndRefreshProductionOrder(
            ProductionOrder, "Production Order Status"::Released, "Prod. Order Source Type"::Item, Item."No.", 1);

        // [THEN] The Prod. Order Routing line resolves to the vendor-currency price (20), converted 1:1 to LCY.
        ProdOrderRoutingLine.SetRange("Prod. Order No.", ProductionOrder."No.");
        ProdOrderRoutingLine.FindFirst();
        Assert.AreEqual(
            VendorCurrencyPrice, ProdOrderRoutingLine."Direct Unit Cost",
            'Prod. Order Routing Direct Unit Cost must use the vendor-currency subcontractor price, not the blank/LCY one.');
        Assert.AreEqual(
            VendorCurrencyPrice, ProdOrderRoutingLine."Unit Cost per",
            'Prod. Order Routing Unit Cost per must use the vendor-currency subcontractor price, not the blank/LCY one.');
    end;

    local procedure CreateItemVendorAndSubcontractingWorkCenter(var Item: Record Item; var Vendor: Record Vendor; var WorkCenter: Record "Work Center")
    begin
        LibraryInventory.CreateItem(Item);
        LibraryPurchase.CreateVendor(Vendor);
        LibraryManufacturing.CreateWorkCenter(WorkCenter);
        WorkCenter.Validate("Subcontractor No.", Vendor."No.");
        WorkCenter.Validate("Indirect Cost %", 0);
        WorkCenter.Validate("Overhead Rate", 0);
        WorkCenter.Modify(true);
    end;

    local procedure CreateSubcontractingItemWithSingleOperationRouting(var Item: Record Item; var Vendor: Record Vendor; var WorkCenter: Record "Work Center"; VendorCurrencyCode: Code[10])
    var
        RoutingNo: Code[20];
    begin
        Vendor.Get(LibraryMfgManagement.CreateSubcontractorWithCurrency(VendorCurrencyCode));

        LibraryMfgManagement.CreateWorkCenterWithCalendar(WorkCenter, 0);
        WorkCenter.Validate("Subcontractor No.", Vendor."No.");
        WorkCenter.Validate("Indirect Cost %", 0);
        WorkCenter.Validate("Overhead Rate", 0);
        WorkCenter.Modify(true);

        RoutingNo := CreateCertifiedSubcontractingRouting(WorkCenter."No.");

        LibraryInventory.CreateItem(Item);
        Item.Validate("Replenishment System", Item."Replenishment System"::"Prod. Order");
        Item.Validate("Routing No.", RoutingNo);
        Item.Modify(true);
    end;

    local procedure CreateCertifiedSubcontractingRouting(WorkCenterNo: Code[20]): Code[20]
    var
        RoutingHeader: Record "Routing Header";
        RoutingLine: Record "Routing Line";
        CapacityUnitOfMeasure: Record "Capacity Unit of Measure";
    begin
        LibraryManufacturing.CreateRoutingHeader(RoutingHeader, RoutingHeader.Type::Serial);

#pragma warning disable AA0210
        CapacityUnitOfMeasure.SetRange(Type, CapacityUnitOfMeasure.Type::Minutes);
#pragma warning restore AA0210
        CapacityUnitOfMeasure.FindFirst();

        LibraryManufacturing.CreateRoutingLineSetup(RoutingLine, RoutingHeader, WorkCenterNo, '10', 0, 1);
        RoutingLine.Validate("Run Time Unit of Meas. Code", CapacityUnitOfMeasure.Code);
        RoutingLine.Validate("Setup Time Unit of Meas. Code", CapacityUnitOfMeasure.Code);
        RoutingLine.Modify(true);

        RoutingHeader.Validate(Status, RoutingHeader.Status::Certified);
        RoutingHeader.Modify(true);
        exit(RoutingHeader."No.");
    end;

    local procedure StageInSubcontractorPrice(var InSubcontractorPrice: Record "Subcontractor Price"; Vendor: Record Vendor; WorkCenter: Record "Work Center"; Item: Record Item; VariantCode: Code[10]; StandardTaskCode: Code[10])
    begin
        // Mirrors how SetSubcontractorPriceForPriceCalculation stages the lookup record for a routing line.
        InSubcontractorPrice.Init();
        InSubcontractorPrice."Vendor No." := Vendor."No.";
        InSubcontractorPrice."Item No." := Item."No.";
        InSubcontractorPrice."Standard Task Code" := StandardTaskCode;
        InSubcontractorPrice."Work Center No." := WorkCenter."No.";
        InSubcontractorPrice."Variant Code" := VariantCode;
        InSubcontractorPrice."Unit of Measure Code" := Item."Base Unit of Measure";
        InSubcontractorPrice."Starting Date" := WorkDate();
        InSubcontractorPrice."Currency Code" := '';
    end;

    local procedure StageRequisitionLine(var RequisitionLine: Record "Requisition Line"; Vendor: Record Vendor; WorkCenter: Record "Work Center"; Item: Record Item; VariantCode: Code[10]; StandardTaskCode: Code[10])
    begin
        RequisitionLine.Init();
        RequisitionLine.Type := RequisitionLine.Type::Item;
        RequisitionLine."No." := Item."No.";
        RequisitionLine."Vendor No." := Vendor."No.";
        RequisitionLine."Work Center No." := WorkCenter."No.";
        RequisitionLine."Variant Code" := VariantCode;
        RequisitionLine."Subc. Standard Task Code" := StandardTaskCode;
        RequisitionLine."Unit of Measure Code" := Item."Base Unit of Measure";
        RequisitionLine."Currency Code" := '';
        RequisitionLine."Order Date" := WorkDate();
        RequisitionLine.Quantity := 1;
    end;

    [Test]
    procedure FactboxCountsBlankUoMPriceWhenPurchLineHasUoM()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcontractorPrice: Record "Subcontractor Price";
        PurchaseLine: Record "Purchase Line";
        SubcPurchFactboxMgmt: Codeunit "Subc. Purch. Factbox Mgmt.";
    begin
        // [SCENARIO] A subcontractor price with a blank Unit of Measure Code must be counted
        // in the Purchase Order FactBox when the purchase line specifies a Unit of Measure Code.
        // Previously, the FactBox used SetRange on Unit of Measure Code (exact match), so a
        // blank-UoM price was invisible whenever the purchase line had a specific UoM.
        Initialize();

        // [GIVEN] An item, vendor, and work center linked as a subcontractor.
        LibraryInventory.CreateItem(Item);
        LibraryPurchase.CreateVendor(Vendor);
        LibraryManufacturing.CreateWorkCenter(WorkCenter);
        WorkCenter.Validate("Subcontractor No.", Vendor."No.");
        WorkCenter.Modify(true);

        // [GIVEN] A subcontractor price recorded with a blank Unit of Measure Code.
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), '', 0, '');

        // [GIVEN] A purchase line for the same item/vendor/work center with a specific UoM.
        PurchaseLine.Init();
        PurchaseLine.Type := PurchaseLine.Type::Item;
        PurchaseLine."No." := Item."No.";
        PurchaseLine."Buy-from Vendor No." := Vendor."No.";
        PurchaseLine."Work Center No." := WorkCenter."No.";
        PurchaseLine."Unit of Measure Code" := Item."Base Unit of Measure";
        PurchaseLine."Currency Code" := '';
        PurchaseLine."Variant Code" := '';

        // [WHEN] The FactBox counts applicable subcontractor prices.
        // [THEN] The blank-UoM price is counted even though the purchase line has a specific UoM.
        Assert.AreEqual(
            1, SubcPurchFactboxMgmt.CalcNoOfPurchasePrices(PurchaseLine),
            'A subcontractor price with blank Unit of Measure must appear in the FactBox when the purchase line has a specific UoM.');
    end;


    local procedure CreateUOMCodeSortingAfter(BaseUOMCode: Code[10]): Code[10]
    var
        UnitOfMeasure: Record "Unit of Measure";
        LibraryUtility: Codeunit "Library - Utility";
        NewCode: Code[10];
    begin
        // LibraryInventory.CreateUnitOfMeasureCode generates a hex-only code (truncated GUID), so
        // any code with a 'Z' prefix is guaranteed to sort after it. This makes the multi-UoM test
        // deterministic — without the fix, FindLast() picks the alt UoM row.
        repeat
            NewCode := CopyStr('Z' + LibraryUtility.GenerateGUID(), 1, MaxStrLen(NewCode));
        until not UnitOfMeasure.Get(NewCode);
        UnitOfMeasure.Init();
        UnitOfMeasure.Code := NewCode;
        UnitOfMeasure.Description := NewCode;
        UnitOfMeasure.Insert(true);
        if UnitOfMeasure.Code <= BaseUOMCode then
            Error('Test setup: generated UoM code %1 must sort after base UoM code %2.', UnitOfMeasure.Code, BaseUOMCode);
        exit(UnitOfMeasure.Code);
    end;

    local procedure Initialize()
    begin
        LibraryTestInitialize.OnTestInitialize(Codeunit::"Subc. Pricing Test");
        LibrarySetupStorage.Restore();

        SubcontractingMgmtLibrary.Initialize();
        SubcontractingMgmtLibrary.UpdateSubMgmtSetup_ComponentAtLocation("Components at Location"::Purchase);
        LibraryMfgManagement.CreateSubcontractingReqWkshTemplateAndNameAndUpdateSetup();
        LibraryVariableStorage.Clear();

        LibraryMfgManagement.Initialize();

        if IsInitialized then
            exit;
        LibraryTestInitialize.OnBeforeTestSuiteInitialize(Codeunit::"Subc. Pricing Test");

        SubSetupLibrary.InitSetupFields();
        LibraryERMCountryData.CreateVATData();
        SubSetupLibrary.InitialSetupForGenProdPostingGroup();

        IsInitialized := true;
        Commit();

        LibraryTestInitialize.OnAfterTestSuiteInitialize(Codeunit::"Subc. Pricing Test");
    end;

    [Test]
    [HandlerFunctions('DetailedCalculationRequestPageHandler')]
    procedure DetailedCalculationReportUsesSubcontractorPricing()
    var
        Item: Record Item;
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        RoutingHeader: Record "Routing Header";
        RoutingLine: Record "Routing Line";
        SubcontractorPrice: Record "Subcontractor Price";
        SubcPriceAmount: Decimal;
        WorkCenterDirectCost: Decimal;
        XmlParameters: Text;
    begin
        // [SCENARIO 638464] Report "Detailed Calculation" must use subcontractor pricing for
        // work centers with a subcontractor when the Subcontracting app is installed, via
        // the OnAfterGetRecordRoutingLineOnBeforeCalcRoutingCostPerUnit event.
        Initialize();

        // [GIVEN] Item with a routing that has a single Work Center operation.
        LibraryInventory.CreateItem(Item);
        LibraryPurchase.CreateVendor(Vendor);
        WorkCenterDirectCost := 50;
        LibraryManufacturing.CreateWorkCenter(WorkCenter);
        WorkCenter.Validate("Direct Unit Cost", WorkCenterDirectCost);
        WorkCenter.Validate("Subcontractor No.", Vendor."No.");
        WorkCenter.Validate("Indirect Cost %", 0);
        WorkCenter.Validate("Overhead Rate", 0);
        WorkCenter.Validate("Unit Cost", WorkCenterDirectCost);
        WorkCenter.Modify(true);

        LibraryManufacturing.CreateRoutingHeader(RoutingHeader, RoutingHeader.Type::Serial);
        LibraryManufacturing.CreateRoutingLine(RoutingHeader, RoutingLine, '', '10', RoutingLine.Type::"Work Center", WorkCenter."No.");
        RoutingLine.Validate("Run Time", 1);
        RoutingLine.Modify(true);
        RoutingHeader.Validate(Status, RoutingHeader.Status::Certified);
        RoutingHeader.Modify(true);

        Item.Validate("Routing No.", RoutingHeader."No.");
        Item.Validate("Lot Size", 1);
        Item.Modify(true);

        // [GIVEN] A subcontractor price of 200 for this item/work center (different from WorkCenter."Direct Unit Cost" of 50).
        SubcPriceAmount := 200;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", SubcPriceAmount);
        SubcontractorPrice.Modify(true);

        // [WHEN] Run the "Detailed Calculation" report (BaseApp 99000756) for this item.
        Commit();
        Item.SetRecFilter();
        XmlParameters := Report.RunRequestPage(Report::"Detailed Calculation");
        LibraryReportDataset.RunReportAndLoad(Report::"Detailed Calculation", Item, XmlParameters);

        // [THEN] The ProdUnitCost in the report dataset equals the subcontractor price (200),
        // not the Work Center's generic Direct Unit Cost (50).
        LibraryReportDataset.AssertElementWithValueExists('ProdUnitCost', SubcPriceAmount);
    end;

    [RequestPageHandler]
    procedure DetailedCalculationRequestPageHandler(var DetailedCalculationRequestPage: TestRequestPage "Detailed Calculation")
    begin
        // Empty handler used to close the request page. We use default settings.
    end;

    [Test]
    procedure CreatedSubcPurchLineUsesPriceForBackwardScheduledOrderDate()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseLine: Record "Purchase Line";
        EarlierPrice: Decimal;
        LaterPrice: Decimal;
    begin
        // [SCENARIO 648535] A new subcontracting purchase line uses the price valid on its backward-scheduled order date
        Initialize();

        // [GIVEN] A subcontracting operation with adjacent prices before and from WorkDate
        CreateDateEffectiveSubcontractingScenario(Item, ProductionOrder, ProdOrderRoutingLine, EarlierPrice, LaterPrice);

        // [WHEN] A purchase order is created for the subcontracting operation
        CreateSubcontractingPurchaseLine(PurchaseLine, ProdOrderRoutingLine, Item."No.", ProductionOrder."No.");

        // [THEN] The purchase line uses the earlier price valid on its backward-scheduled order date
        Assert.IsTrue(PurchaseLine."Order Date" < WorkDate(), 'The purchase line order date must be backward-scheduled before WorkDate.');
        Assert.AreEqual(EarlierPrice, PurchaseLine."Direct Unit Cost", 'The purchase line must use the subcontractor price valid on its order date.');
    end;

    [Test]
    procedure ReqWkshCreatedSubcPurchLineUsesPriceForBackwardScheduledOrderDate()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseLine: Record "Purchase Line";
        ReqWkshTemplate: Record "Req. Wksh. Template";
        RequisitionLine: Record "Requisition Line";
        RequisitionWkshName: Record "Requisition Wksh. Name";
        EarlierPrice: Decimal;
        LaterPrice: Decimal;
    begin
        // [SCENARIO 648535] A worksheet-created subcontracting purchase line uses the price valid on its backward-scheduled order date
        Initialize();

        // [GIVEN] A subcontracting operation with adjacent prices before and from WorkDate
        CreateDateEffectiveSubcontractingScenario(Item, ProductionOrder, ProdOrderRoutingLine, EarlierPrice, LaterPrice);
        SubcontractingMgmtLibrary.CreateReqWkshTemplateAndName(ReqWkshTemplate, RequisitionWkshName);
        CalculateSubcontractsAndFindReqLine(RequisitionWkshName, ProductionOrder."No.", RequisitionLine);

        // [WHEN] The worksheet action is carried out for the subcontracting operation
        CarryOutSubcontractingAction(RequisitionLine);

        // [THEN] The purchase line uses the earlier price valid on its backward-scheduled order date
        FindSubcPurchLineForProdOrder(PurchaseLine, Item."No.", ProductionOrder."No.");
        Assert.IsTrue(PurchaseLine."Order Date" < WorkDate(), 'The purchase line order date must be backward-scheduled before WorkDate.');
        Assert.AreEqual(EarlierPrice, PurchaseLine."Direct Unit Cost", 'The purchase line must use the subcontractor price valid on its order date.');
    end;

    [Test]
    procedure ExpectedReceiptDateChangeRepricesBackwardScheduledSubcPurchLine()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseLine: Record "Purchase Line";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        NewExpectedReceiptDate: Date;
        EarlierPrice: Decimal;
        LaterPrice: Decimal;
    begin
        // [SCENARIO 648535] Rescheduling a subcontracting purchase line reapplies the date-effective price
        Initialize();

        // [GIVEN] A backward-scheduled subcontracting purchase line using the price valid before WorkDate
        CreateDateEffectiveSubcontractingScenario(Item, ProductionOrder, ProdOrderRoutingLine, EarlierPrice, LaterPrice);
        CreateSubcontractingPurchaseLine(PurchaseLine, ProdOrderRoutingLine, Item."No.", ProductionOrder."No.");
        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);
        Assert.AreEqual(EarlierPrice, PurchaseLine."Direct Unit Cost", 'The purchase line must initially use the earlier subcontractor price.');
        NewExpectedReceiptDate := CalcDate('<20D>', WorkDate());

        // [WHEN] Expected Receipt Date is moved so Planned Receipt Date backward-schedules Order Date into the later price period
        PurchaseLine.Validate("Expected Receipt Date", NewExpectedReceiptDate);

        // [THEN] The purchase line uses the later price valid on its rescheduled order date
        Assert.IsTrue(PurchaseLine."Order Date" >= WorkDate(), 'The rescheduled purchase line order date must be on or after WorkDate.');
        Assert.AreEqual(LaterPrice, PurchaseLine."Direct Unit Cost", 'The purchase line must use the subcontractor price valid on its rescheduled order date.');
    end;

    [Test]
    procedure PlannedReceiptDateChangeRepricesSubcPurchLine()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseLine: Record "Purchase Line";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        NewPlannedReceiptDate: Date;
        EarlierPrice: Decimal;
        LaterPrice: Decimal;
    begin
        // [SCENARIO 648535] Changing Planned Receipt Date directly reapplies the price for the resulting order date
        Initialize();

        // [GIVEN] A backward-scheduled subcontracting purchase line using the price valid before WorkDate
        CreateDateEffectiveSubcontractingScenario(Item, ProductionOrder, ProdOrderRoutingLine, EarlierPrice, LaterPrice);
        CreateSubcontractingPurchaseLine(PurchaseLine, ProdOrderRoutingLine, Item."No.", ProductionOrder."No.");
        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);
        Assert.AreEqual(EarlierPrice, PurchaseLine."Direct Unit Cost", 'The purchase line must initially use the earlier subcontractor price.');
        NewPlannedReceiptDate := CalcDate('<20D>', WorkDate());

        // [WHEN] Planned Receipt Date is changed directly
        PurchaseLine.Validate("Planned Receipt Date", NewPlannedReceiptDate);

        // [THEN] The purchase line uses the later price valid on the resulting order date
        Assert.IsTrue(PurchaseLine."Order Date" >= WorkDate(), 'The resulting purchase line order date must be on or after WorkDate.');
        Assert.AreEqual(LaterPrice, PurchaseLine."Direct Unit Cost", 'The purchase line must use the subcontractor price valid on the resulting order date.');
    end;

    [Test]
    procedure OrderDateChangeRepricesSubcPurchLine()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseLine: Record "Purchase Line";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        EarlierPrice: Decimal;
        LaterPrice: Decimal;
    begin
        // [SCENARIO 648535] Changing a subcontracting purchase line order date reapplies the date-effective price
        Initialize();

        // [GIVEN] A backward-scheduled subcontracting purchase line using the price valid before WorkDate
        CreateDateEffectiveSubcontractingScenario(Item, ProductionOrder, ProdOrderRoutingLine, EarlierPrice, LaterPrice);
        CreateSubcontractingPurchaseLine(PurchaseLine, ProdOrderRoutingLine, Item."No.", ProductionOrder."No.");
        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);
        Assert.AreEqual(EarlierPrice, PurchaseLine."Direct Unit Cost", 'The purchase line must initially use the earlier subcontractor price.');

        // [WHEN] Order Date is changed to WorkDate
        PurchaseLine.Validate("Order Date", WorkDate());

        // [THEN] The purchase line uses the later price valid from WorkDate
        Assert.AreEqual(LaterPrice, PurchaseLine."Direct Unit Cost", 'The purchase line must use the subcontractor price valid on its changed order date.');
    end;

    [Test]
    procedure NoMatchPriceListPreservesCalculatedWorksheetCostForTimeBasedRouting()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseLine: Record "Purchase Line";
        ReqWkshTemplate: Record "Req. Wksh. Template";
        RequisitionLine: Record "Requisition Line";
        RequisitionWkshName: Record "Requisition Wksh. Name";
        RoutingHeader: Record "Routing Header";
        RoutingLine: Record "Routing Line";
        SubcontractorPrice: Record "Subcontractor Price";
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        CalculatedWorksheetCost: Decimal;
        RoutingRate: Decimal;
    begin
        // [SCENARIO 648535] Carry-out preserves the calculated time-based cost when no price matches
        Initialize();

        CreateSubcontractingItemWithSingleOperationRouting(Item, Vendor, WorkCenter, '');
        WorkCenter.Validate("Direct Unit Cost", LibraryRandom.RandDecInRange(10, 100, 2));
        WorkCenter.Modify(true);
        RoutingHeader.Get(Item."Routing No.");
        RoutingHeader.Validate(Status, RoutingHeader.Status::New);
        RoutingHeader.Modify(true);
        RoutingLine.SetRange("Routing No.", Item."Routing No.");
        RoutingLine.FindFirst();
        RoutingLine.Validate("Run Time", 5);
        RoutingLine.Modify(true);
        RoutingHeader.Validate(Status, RoutingHeader.Status::Certified);
        RoutingHeader.Modify(true);

        SubcontractorPrice.SetRange("Vendor No.", Vendor."No.");
        SubcontractorPrice.SetRange("Work Center No.", WorkCenter."No.");
        SubcontractorPrice.SetRange("Item No.", Item."No.");
        Assert.IsTrue(SubcontractorPrice.IsEmpty(), 'Test setup expects no subcontractor prices for the operation.');

        LibraryManufacturing.CreateProductionOrder(
            ProductionOrder, "Production Order Status"::Released, Item, '', '', 1, WorkDate());
        ProdOrderRoutingLine.SetRange(Status, ProductionOrder.Status);
        ProdOrderRoutingLine.SetRange("Prod. Order No.", ProductionOrder."No.");
        ProdOrderRoutingLine.SetRange("Work Center No.", WorkCenter."No.");
        ProdOrderRoutingLine.FindFirst();
        RoutingRate := ProdOrderRoutingLine."Direct Unit Cost";

        SubcontractingMgmtLibrary.CreateReqWkshTemplateAndName(ReqWkshTemplate, RequisitionWkshName);
        CalculateSubcontractsAndFindReqLine(RequisitionWkshName, ProductionOrder."No.", RequisitionLine);
        CalculatedWorksheetCost := RequisitionLine."Direct Unit Cost";
        Assert.AreNotEqual(RoutingRate, CalculatedWorksheetCost, 'Test setup expects a time-based cost different from the routing rate.');

        CarryOutSubcontractingAction(RequisitionLine);

        FindSubcPurchLineForProdOrder(PurchaseLine, Item."No.", ProductionOrder."No.");
        Assert.AreEqual(
            CalculatedWorksheetCost, PurchaseLine."Direct Unit Cost",
            'Carry-out must preserve the calculated worksheet cost when no subcontractor price matches.');
    end;

    [Test]
    procedure NoMatchPriceListFallbackUsesCalculatedCostForUnitsAndTime()
    var
        ProdOrderLine: Record "Prod. Order Line";
        PurchaseLine: Record "Purchase Line";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        TimeDirectUnitCost: Decimal;
        UnitsDirectUnitCost: Decimal;
    begin
        // [SCENARIO 648535] Direct purchase-line repricing without a matching price uses the
        // standard subcontracting calculation for both unit- and time-based operations.
        Initialize();

        CreateNoPriceSubcontractingPurchaseLine(
            PurchaseLine, ProdOrderLine, Enum::"Unit Cost Calculation Type"::Units);
        UnitsDirectUnitCost := PurchaseLine."Direct Unit Cost";
        Assert.AreNotEqual(0, UnitsDirectUnitCost, 'Test setup expects a nonzero Units Direct Unit Cost.');
        PurchaseLine."Direct Unit Cost" := 0;

        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);

        Assert.AreEqual(
            UnitsDirectUnitCost, PurchaseLine."Direct Unit Cost",
            'The Units fallback must match the Direct Unit Cost calculated when the purchase line was created.');

        CreateNoPriceSubcontractingPurchaseLine(
            PurchaseLine, ProdOrderLine, Enum::"Unit Cost Calculation Type"::Time);
        TimeDirectUnitCost := PurchaseLine."Direct Unit Cost";
        Assert.AreNotEqual(0, TimeDirectUnitCost, 'Test setup expects a nonzero Time Direct Unit Cost.');
        Assert.AreNotEqual(UnitsDirectUnitCost, TimeDirectUnitCost, 'Test setup expects Units and Time calculations to produce different costs.');
        PurchaseLine."Direct Unit Cost" := 0;

        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);

        Assert.AreEqual(
            TimeDirectUnitCost, PurchaseLine."Direct Unit Cost",
            'The Time fallback must match the Direct Unit Cost calculated when the purchase line was created.');
    end;

    [Test]
    procedure FinalPurchaseLineDateAppliesPriceWhenReqLineDateHasNoPrice()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        PurchaseLine: Record "Purchase Line";
        ReqWkshTemplate: Record "Req. Wksh. Template";
        RequisitionLine: Record "Requisition Line";
        RequisitionWkshName: Record "Requisition Wksh. Name";
        SubcontractorPrice: Record "Subcontractor Price";
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        FinalDatePrice: Decimal;
    begin
        // [SCENARIO 648535] An automatically calculated worksheet fallback is repriced when
        // the final purchase-line date has an applicable subcontractor price.
        Initialize();

        CreateSubcontractingItemWithSingleOperationRouting(Item, Vendor, WorkCenter, '');
        WorkCenter.Validate("Direct Unit Cost", LibraryRandom.RandDecInRange(10, 100, 2));
        WorkCenter.Modify(true);
        Evaluate(Item."Lead Time Calculation", '<5D>');
        Item.Modify(true);
        LibraryManufacturing.CreateProductionOrder(
            ProductionOrder, "Production Order Status"::Released, Item, '', '', 1, WorkDate());
        SubcontractingMgmtLibrary.CreateReqWkshTemplateAndName(ReqWkshTemplate, RequisitionWkshName);
        CalculateSubcontractsAndFindReqLine(RequisitionWkshName, ProductionOrder."No.", RequisitionLine);
        RequisitionLine.Validate("Order Date", WorkDate());
        RequisitionLine.Modify(true);
        FinalDatePrice := LibraryRandom.RandDecInRange(100, 200, 2);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '',
            CalcDate('<-1M>', WorkDate()), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Ending Date", CalcDate('<-1D>', WorkDate()));
        SubcontractorPrice.Validate("Direct Unit Cost", FinalDatePrice);
        SubcontractorPrice.Modify(true);

        CarryOutSubcontractingAction(RequisitionLine);

        FindSubcPurchLineForProdOrder(PurchaseLine, Item."No.", ProductionOrder."No.");
        Assert.IsTrue(
            PurchaseLine."Order Date" < WorkDate(),
            'The final purchase-line Order Date must be in the price validity period.');
        Assert.AreEqual(
            FinalDatePrice, PurchaseLine."Direct Unit Cost",
            'The price valid on the final purchase-line date must replace the automatic worksheet fallback.');
    end;

    [Test]
    procedure ReqLinePriceAboveMinimumQuantityPreservesCalculatedFallback()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ReqWkshTemplate: Record "Req. Wksh. Template";
        RequisitionLine: Record "Requisition Line";
        RequisitionWkshName: Record "Requisition Wksh. Name";
        SubcontractorPrice: Record "Subcontractor Price";
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        CalculatedFallbackCost: Decimal;
    begin
        // [SCENARIO 648535] A requisition-line price whose minimum quantity is too high does
        // not replace the standard calculated subcontracting cost with zero.
        Initialize();

        CreateSubcontractingItemWithSingleOperationRouting(Item, Vendor, WorkCenter, '');
        WorkCenter.Validate("Direct Unit Cost", LibraryRandom.RandDecInRange(10, 100, 2));
        WorkCenter.Modify(true);
        LibraryManufacturing.CreateProductionOrder(
            ProductionOrder, "Production Order Status"::Released, Item, '', '', 1, WorkDate());
        SubcontractingMgmtLibrary.CreateReqWkshTemplateAndName(ReqWkshTemplate, RequisitionWkshName);
        CalculateSubcontractsAndFindReqLine(RequisitionWkshName, ProductionOrder."No.", RequisitionLine);
        CalculatedFallbackCost := RequisitionLine."Direct Unit Cost";
        Assert.AreNotEqual(0, CalculatedFallbackCost, 'Test setup expects a nonzero calculated fallback cost.');
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '',
            RequisitionLine."Order Date", RequisitionLine."Unit of Measure Code",
            RequisitionLine.Quantity + 1, RequisitionLine."Currency Code");
        SubcontractorPrice.Validate("Direct Unit Cost", CalculatedFallbackCost * 2);
        SubcontractorPrice.Modify(true);

        SubcPriceManagement.GetSubcPriceForReqLine(RequisitionLine, '');

        Assert.AreEqual(
            CalculatedFallbackCost, RequisitionLine."Direct Unit Cost",
            'A requisition-line price above the quantity threshold must preserve the calculated fallback.');
    end;

    [Test]
    procedure NoApplicableMinimumQuantityPriceUsesCalculatedFallback()
    var
        ProdOrderLine: Record "Prod. Order Line";
        PurchaseLine: Record "Purchase Line";
        SubcontractorPrice: Record "Subcontractor Price";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        CalculatedFallbackCost: Decimal;
    begin
        // [SCENARIO 648535] A date-compatible price with a minimum quantity above the purchase
        // quantity is not an applicable price and must not replace the calculated fallback with zero.
        Initialize();

        CreateNoPriceSubcontractingPurchaseLine(
            PurchaseLine, ProdOrderLine, Enum::"Unit Cost Calculation Type"::Time);
        CalculatedFallbackCost := PurchaseLine."Direct Unit Cost";
        Assert.AreNotEqual(0, CalculatedFallbackCost, 'Test setup expects a nonzero calculated fallback cost.');
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, PurchaseLine."Work Center No.", PurchaseLine."Buy-from Vendor No.",
            PurchaseLine."No.", '', PurchaseLine."Variant Code", PurchaseLine."Order Date",
            PurchaseLine."Unit of Measure Code", PurchaseLine.Quantity + 1, PurchaseLine."Currency Code");
        SubcontractorPrice.Validate("Direct Unit Cost", CalculatedFallbackCost * 2);
        SubcontractorPrice.Modify(true);
        PurchaseLine."Direct Unit Cost" := 0;

        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);

        Assert.AreEqual(
            CalculatedFallbackCost, PurchaseLine."Direct Unit Cost",
            'A price above the purchase quantity threshold must not suppress the calculated fallback.');
    end;

    [Test]
    procedure ApplicableZeroPriceIsNotReplacedByCalculatedFallback()
    var
        ProdOrderLine: Record "Prod. Order Line";
        PurchaseLine: Record "Purchase Line";
        SubcontractorPrice: Record "Subcontractor Price";
        SubcPriceManagement: Codeunit "Subc. Price Management";
    begin
        // [SCENARIO 648535] A matched price tier can intentionally have a zero direct unit cost.
        Initialize();

        CreateNoPriceSubcontractingPurchaseLine(
            PurchaseLine, ProdOrderLine, Enum::"Unit Cost Calculation Type"::Time);
        Assert.AreNotEqual(0, PurchaseLine."Direct Unit Cost", 'Test setup expects a nonzero calculated fallback cost.');
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, PurchaseLine."Work Center No.", PurchaseLine."Buy-from Vendor No.",
            PurchaseLine."No.", '', PurchaseLine."Variant Code", PurchaseLine."Order Date",
            PurchaseLine."Unit of Measure Code", 0, PurchaseLine."Currency Code");

        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);

        Assert.AreEqual(
            0, PurchaseLine."Direct Unit Cost",
            'An applicable zero price must not be replaced by the calculated fallback.');
    end;

    [Test]
    procedure NoMatchPriceListFallbackHandlesZeroExpectedOutputQuantity()
    var
        ProdOrderLine: Record "Prod. Order Line";
        PurchaseLine: Record "Purchase Line";
        SubcPriceManagement: Codeunit "Subc. Price Management";
    begin
        // [SCENARIO 648535] Time-based fallback pricing returns zero instead of dividing by
        // zero when the production order has no expected operation output quantity.
        Initialize();

        CreateNoPriceSubcontractingPurchaseLine(
            PurchaseLine, ProdOrderLine, Enum::"Unit Cost Calculation Type"::Time);
        ProdOrderLine.Validate(Quantity, 0);
        ProdOrderLine.Modify(true);

        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);

        Assert.AreEqual(
            0, PurchaseLine."Direct Unit Cost",
            'The no-price fallback must be zero when expected operation output quantity is zero.');
    end;

    [Test]
    procedure MinimumAmountPricingSkipsAdjustmentAtZeroQuantity()
    var
        ProdOrderLine: Record "Prod. Order Line";
        PurchaseLine: Record "Purchase Line";
        SubcontractorPrice: Record "Subcontractor Price";
        SubcPriceManagement: Codeunit "Subc. Price Management";
        PriceListDirectUnitCost: Decimal;
    begin
        // [SCENARIO 648535] A matched price with a Minimum Amount must not divide by a zero
        // purchase quantity; the price list Direct Unit Cost is used unadjusted instead.
        Initialize();

        CreateNoPriceSubcontractingPurchaseLine(
            PurchaseLine, ProdOrderLine, Enum::"Unit Cost Calculation Type"::Time);
        PriceListDirectUnitCost := LibraryRandom.RandDecInRange(10, 100, 2);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, PurchaseLine."Work Center No.", PurchaseLine."Buy-from Vendor No.",
            PurchaseLine."No.", '', PurchaseLine."Variant Code", PurchaseLine."Order Date",
            PurchaseLine."Unit of Measure Code", 0, PurchaseLine."Currency Code");
        SubcontractorPrice.Validate("Direct Unit Cost", PriceListDirectUnitCost);
        SubcontractorPrice.Validate("Minimum Amount", PriceListDirectUnitCost * 1000);
        SubcontractorPrice.Modify(true);
        PurchaseLine.Quantity := 0;

        SubcPriceManagement.GetSubcPriceForPurchLine(PurchaseLine);

        Assert.AreEqual(
            PriceListDirectUnitCost, PurchaseLine."Direct Unit Cost",
            'A zero purchase quantity must not trigger a minimum-amount division and must keep the price list cost.');
    end;

    [Test]
    procedure ManualWorksheetDirectUnitCostOverridePreservedOnCarryOut()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseLine: Record "Purchase Line";
        ReqWkshTemplate: Record "Req. Wksh. Template";
        RequisitionLine: Record "Requisition Line";
        RequisitionWkshName: Record "Requisition Wksh. Name";
        EarlierPrice: Decimal;
        LaterPrice: Decimal;
        ManualCost: Decimal;
    begin
        // [SCENARIO 648535] Carry-out preserves a manually overridden worksheet cost
        Initialize();

        CreateDateEffectiveSubcontractingScenario(Item, ProductionOrder, ProdOrderRoutingLine, EarlierPrice, LaterPrice);
        SubcontractingMgmtLibrary.CreateReqWkshTemplateAndName(ReqWkshTemplate, RequisitionWkshName);
        CalculateSubcontractsAndFindReqLine(RequisitionWkshName, ProductionOrder."No.", RequisitionLine);
        ManualCost := EarlierPrice + LaterPrice;
        RequisitionLine.Validate("Direct Unit Cost", ManualCost);
        RequisitionLine.Modify(true);

        CarryOutSubcontractingAction(RequisitionLine);

        FindSubcPurchLineForProdOrder(PurchaseLine, Item."No.", ProductionOrder."No.");
        Assert.AreEqual(
            ManualCost, PurchaseLine."Direct Unit Cost",
            'Carry-out must preserve a manually overridden worksheet Direct Unit Cost.');
    end;

    [Test]
    procedure DateEditsOnReleasedSubcPurchOrderLinePreserveFinancialTerms()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseHeader: Record "Purchase Header";
        PurchaseLine: Record "Purchase Line";
        EarlierPrice: Decimal;
        LaterPrice: Decimal;
        NewPlannedReceiptDate: Date;
        OriginalDirectUnitCost: Decimal;
        OriginalLineDiscount: Decimal;
    begin
        // [SCENARIO 648535] Scheduling edits remain allowed on a released subcontracting order
        Initialize();

        CreateDateEffectiveSubcontractingScenario(Item, ProductionOrder, ProdOrderRoutingLine, EarlierPrice, LaterPrice);
        CreateSubcontractingPurchaseLine(PurchaseLine, ProdOrderRoutingLine, Item."No.", ProductionOrder."No.");
        OriginalDirectUnitCost := PurchaseLine."Direct Unit Cost";
        OriginalLineDiscount := PurchaseLine."Line Discount %";
        PurchaseHeader.Get(PurchaseLine."Document Type", PurchaseLine."Document No.");
        LibraryPurchase.ReleasePurchaseDocument(PurchaseHeader);
        PurchaseLine.Get(PurchaseLine."Document Type", PurchaseLine."Document No.", PurchaseLine."Line No.");

        NewPlannedReceiptDate := CalcDate('<20D>', WorkDate());
        PurchaseLine.Validate("Planned Receipt Date", NewPlannedReceiptDate);

        PurchaseLine.TestField("Planned Receipt Date", NewPlannedReceiptDate);
        Assert.AreEqual(OriginalDirectUnitCost, PurchaseLine."Direct Unit Cost", 'A released line must retain its Direct Unit Cost.');
        Assert.AreEqual(OriginalLineDiscount, PurchaseLine."Line Discount %", 'A released line must retain its Line Discount %.');

        PurchaseLine.Validate("Order Date", WorkDate());

        PurchaseLine.TestField("Order Date", WorkDate());
        Assert.AreEqual(OriginalDirectUnitCost, PurchaseLine."Direct Unit Cost", 'A released line must retain its Direct Unit Cost.');
        Assert.AreEqual(OriginalLineDiscount, PurchaseLine."Line Discount %", 'A released line must retain its Line Discount %.');
    end;

    [Test]
    procedure LeadTimeCalculationOnlyOrderDateShiftRepricesSubcPurchLine()
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        PurchaseLine: Record "Purchase Line";
        NewLeadTime: DateFormula;
        EarlierPrice: Decimal;
        LaterPrice: Decimal;
        AlignedDate: Date;
        OriginalPlannedReceiptDate: Date;
    begin
        // [SCENARIO 648535] A lead-time-only Order Date change reapplies date-effective pricing
        Initialize();

        CreateDateEffectiveSubcontractingScenario(Item, ProductionOrder, ProdOrderRoutingLine, EarlierPrice, LaterPrice);
        CreateSubcontractingPurchaseLine(PurchaseLine, ProdOrderRoutingLine, Item."No.", ProductionOrder."No.");
        Assert.AreEqual(EarlierPrice, PurchaseLine."Direct Unit Cost", 'The line must initially use the earlier price.');

        AlignedDate := CalcDate('<20D>', WorkDate());
        PurchaseLine.Validate("Requested Receipt Date", AlignedDate);
        PurchaseLine.Validate("Promised Receipt Date", 0D);
        OriginalPlannedReceiptDate := PurchaseLine."Planned Receipt Date";

        Evaluate(NewLeadTime, '<0D>');
        PurchaseLine.Validate("Lead Time Calculation", NewLeadTime);

        Assert.AreEqual(
            OriginalPlannedReceiptDate, PurchaseLine."Planned Receipt Date",
            'Planned Receipt Date must remain unchanged for a lead-time-only reschedule.');
        Assert.IsTrue(PurchaseLine."Order Date" >= WorkDate(), 'Order Date must move into the later price period.');
        Assert.AreEqual(
            LaterPrice, PurchaseLine."Direct Unit Cost",
            'The line must use the price valid on the resulting Order Date.');
    end;

    local procedure CreateDateEffectiveSubcontractingScenario(var Item: Record Item; var ProductionOrder: Record "Production Order"; var ProdOrderRoutingLine: Record "Prod. Order Routing Line"; var EarlierPrice: Decimal; var LaterPrice: Decimal)
    var
        SubcontractorPrice: Record "Subcontractor Price";
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
    begin
        CreateSubcontractingItemWithSingleOperationRouting(Item, Vendor, WorkCenter, '');
        Evaluate(Item."Lead Time Calculation", '<5D>');
        Item.Modify(true);

        EarlierPrice := 100;
        LaterPrice := 200;
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', CalcDate('<-1M>', WorkDate()), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Ending Date", CalcDate('<-1D>', WorkDate()));
        SubcontractorPrice.Validate("Direct Unit Cost", EarlierPrice);
        SubcontractorPrice.Modify(true);
        Clear(SubcontractorPrice);
        SubcontractingMgmtLibrary.CreateSubContractingPrice(
            SubcontractorPrice, WorkCenter."No.", Vendor."No.", Item."No.", '', '', WorkDate(), Item."Base Unit of Measure", 0, '');
        SubcontractorPrice.Validate("Direct Unit Cost", LaterPrice);
        SubcontractorPrice.Modify(true);

        LibraryManufacturing.CreateProductionOrder(
            ProductionOrder, "Production Order Status"::Released, Item, '', '', 1, WorkDate());
        ProdOrderRoutingLine.SetRange(Status, ProductionOrder.Status);
        ProdOrderRoutingLine.SetRange("Prod. Order No.", ProductionOrder."No.");
        ProdOrderRoutingLine.SetRange("Work Center No.", WorkCenter."No.");
        ProdOrderRoutingLine.FindFirst();
    end;

    local procedure CreateSubcontractingPurchaseLine(var PurchaseLine: Record "Purchase Line"; ProdOrderRoutingLine: Record "Prod. Order Routing Line"; ItemNo: Code[20]; ProdOrderNo: Code[20])
    var
        SubcPurchaseOrderCreator: Codeunit "Subc. Purchase Order Creator";
    begin
        SubcPurchaseOrderCreator.CreateSubcontractingPurchaseOrderFromRoutingLine(ProdOrderRoutingLine);
        FindSubcPurchLineForProdOrder(PurchaseLine, ItemNo, ProdOrderNo);
    end;

    local procedure CreateNoPriceSubcontractingPurchaseLine(var PurchaseLine: Record "Purchase Line"; var ProdOrderLine: Record "Prod. Order Line"; UnitCostCalculation: Enum "Unit Cost Calculation Type")
    var
        Item: Record Item;
        ProductionOrder: Record "Production Order";
        ProdOrderRoutingLine: Record "Prod. Order Routing Line";
        RoutingHeader: Record "Routing Header";
        RoutingLine: Record "Routing Line";
        SubcontractorPrice: Record "Subcontractor Price";
        Vendor: Record Vendor;
        WorkCenter: Record "Work Center";
    begin
        CreateSubcontractingItemWithSingleOperationRouting(Item, Vendor, WorkCenter, '');
        WorkCenter.Validate("Direct Unit Cost", 10);
        WorkCenter.Validate("Unit Cost Calculation", UnitCostCalculation);
        WorkCenter.Modify(true);

        RoutingHeader.Get(Item."Routing No.");
        RoutingHeader.Validate(Status, RoutingHeader.Status::New);
        RoutingHeader.Modify(true);
        RoutingLine.SetRange("Routing No.", Item."Routing No.");
        RoutingLine.FindFirst();
        RoutingLine.Validate("Run Time", 5);
        RoutingLine.Modify(true);
        RoutingHeader.Validate(Status, RoutingHeader.Status::Certified);
        RoutingHeader.Modify(true);

        SubcontractorPrice.SetRange("Vendor No.", Vendor."No.");
        SubcontractorPrice.SetRange("Work Center No.", WorkCenter."No.");
        SubcontractorPrice.SetRange("Item No.", Item."No.");
        Assert.IsTrue(SubcontractorPrice.IsEmpty(), 'Test setup expects no subcontractor prices for the operation.');

        LibraryManufacturing.CreateProductionOrder(
            ProductionOrder, "Production Order Status"::Released, Item, '', '', 1, WorkDate());
        ProdOrderRoutingLine.SetRange(Status, ProductionOrder.Status);
        ProdOrderRoutingLine.SetRange("Prod. Order No.", ProductionOrder."No.");
        ProdOrderRoutingLine.SetRange("Work Center No.", WorkCenter."No.");
        ProdOrderRoutingLine.FindFirst();

        CreateSubcontractingPurchaseLine(PurchaseLine, ProdOrderRoutingLine, Item."No.", ProductionOrder."No.");

        ProdOrderLine.SetRange(Status, ProductionOrder.Status);
        ProdOrderLine.SetRange("Prod. Order No.", ProductionOrder."No.");
        ProdOrderLine.SetRange("Routing No.", ProdOrderRoutingLine."Routing No.");
        ProdOrderLine.SetRange("Routing Reference No.", ProdOrderRoutingLine."Routing Reference No.");
        ProdOrderLine.FindFirst();
    end;


    local procedure CalculateSubcontractsAndFindReqLine(RequisitionWkshName: Record "Requisition Wksh. Name"; ProdOrderNo: Code[20]; var RequisitionLine: Record "Requisition Line")
    var
        SubcCalculateSubContract: Report "Subc. Calculate Subcontracts";
    begin
        Clear(RequisitionLine);
        RequisitionLine."Worksheet Template Name" := RequisitionWkshName."Worksheet Template Name";
        RequisitionLine."Journal Batch Name" := RequisitionWkshName.Name;

        SubcCalculateSubContract.SetWkShLine(RequisitionLine);
        SubcCalculateSubContract.UseRequestPage(false);
        SubcCalculateSubContract.RunModal();

        RequisitionLine.SetRange("Worksheet Template Name", RequisitionWkshName."Worksheet Template Name");
        RequisitionLine.SetRange("Journal Batch Name", RequisitionWkshName.Name);
#pragma warning disable AA0210
        RequisitionLine.SetRange("Prod. Order No.", ProdOrderNo);
#pragma warning restore AA0210
        RequisitionLine.FindFirst();
    end;

    local procedure CarryOutSubcontractingAction(var RequisitionLine: Record "Requisition Line")
    var
        CarryOutActionMsgReq: Report "Carry Out Action Msg. - Req.";
    begin
        CarryOutActionMsgReq.SetReqWkshLine(RequisitionLine);
        CarryOutActionMsgReq.UseRequestPage(false);
        CarryOutActionMsgReq.RunModal();
    end;

    local procedure FindSubcPurchLineForProdOrder(var PurchaseLine: Record "Purchase Line"; ItemNo: Code[20]; ProdOrderNo: Code[20])
    begin
        PurchaseLine.SetRange("Document Type", PurchaseLine."Document Type"::Order);
        PurchaseLine.SetRange(Type, "Purchase Line Type"::Item);
        PurchaseLine.SetRange("No.", ItemNo);
        PurchaseLine.SetRange("Prod. Order No.", ProdOrderNo);
        PurchaseLine.FindFirst();
    end;

    var
        Assert: Codeunit Assert;
        LibraryERM: Codeunit "Library - ERM";
        LibraryERMCountryData: Codeunit "Library - ERM Country Data";
        LibraryInventory: Codeunit "Library - Inventory";
        LibraryManufacturing: Codeunit "Library - Manufacturing";
        LibraryPurchase: Codeunit "Library - Purchase";
        LibraryRandom: Codeunit "Library - Random";
        LibraryReportDataset: Codeunit "Library - Report Dataset";
        LibrarySetupStorage: Codeunit "Library - Setup Storage";
        LibraryTestInitialize: Codeunit "Library - Test Initialize";
        LibraryMfgManagement: Codeunit "Subc. Library Mfg. Management";
        SubcontractingMgmtLibrary: Codeunit "Subc. Management Library";
        LibraryVariableStorage: Codeunit "Library - Variable Storage";
        SubSetupLibrary: Codeunit "Subc. Setup Library";
        IsInitialized: Boolean;
}
