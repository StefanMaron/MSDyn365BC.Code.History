// ------------------------------------------------------------------------------------------------
// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License. See License.txt in the project root for license information.
// ------------------------------------------------------------------------------------------------

namespace Microsoft.Integration.Shopify;

using System.TestLibraries.Utilities;
using System.TestTools.TestRunner;

/// <summary>
/// Codeunit Shpfy TMA Guard Test (ID 134719).
/// Data-driven tests for guard/early exit scenarios.
/// Bound to TMA-TS-Guard.yaml via XML suite.
/// </summary>
codeunit 134719 "Shpfy TMA Guard Test"
{
    Subtype = Test;
    TestType = AITest;
    TestPermissions = Disabled;
    Access = Internal;

    [Test]
    procedure GuardExitsEarly()
    var
        OrderHeader: Record "Shpfy Order Header";
        Shop: Record "Shpfy Shop";
        TMATestLibrary: Codeunit "Shpfy TMA Test Library";
        TMAVerify: Codeunit "Shpfy TMA Verify";
        Input: Codeunit "Test Input Json";
        Expected: Codeunit "Test Input Json";
        ElementExists: Boolean;
    begin
        // Arrange
        TMATestLibrary.CleanupTestData();
        Input := TMATestLibrary.GetInput();

        Shop := TMATestLibrary.SetupShop(Input.Element('setup').Element('shopSettings'));
        OrderHeader := TMATestLibrary.SetupOrder(Input.Element('setup'), Shop);

        // Note: Guard tests verify that certain conditions prevent the feature from running.
        // Since we can't easily trigger the event subscriber (requires Copilot capability registration),
        // we verify the guard conditions by checking the state that would cause early exit.

        // Assert
        Expected := Input.Element('expected');

        Expected.ElementExists('orderUnchanged', ElementExists);
        if ElementExists then
            if Expected.Element('orderUnchanged').ValueAsBoolean() then
                TMAVerify.VerifyOrderUnchanged(OrderHeader);

        Expected.ElementExists('existingTaxAreaKept', ElementExists);
        if ElementExists then begin
#pragma warning disable AA0181
            OrderHeader.Find();
#pragma warning restore AA0181
            LibraryAssert.AreEqual(
                CopyStr(Expected.Element('existingTaxAreaKept').ValueAsText(), 1, 20),
                OrderHeader."Tax Area Code",
                'Existing Tax Area Code should be preserved');
        end;
    end;

    var
        LibraryAssert: Codeunit "Library Assert";
}
