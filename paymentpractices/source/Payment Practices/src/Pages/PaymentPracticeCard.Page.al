page 687 "Payment Practice Card"
{
    PageType = Card;
    SourceTable = "Payment Practice Header";

    layout
    {
        area(Content)
        {
            group(General)
            {
                field("No."; Rec."No.")
                {
                    ApplicationArea = All;
                    ToolTip = 'Specifies the number of the payment practice header.';
                }
                field("Aggregation Type"; Rec."Aggregation Type")
                {
                    ApplicationArea = All;
                    ToolTip = 'Specifies the aggregation type of the payment practice.';
                }
                field("Header Type"; Rec."Header Type")
                {
                    ApplicationArea = All;
                    ToolTip = 'Specifies the source for entries in the payment practice.';
                }
                field("Startind Date"; Rec."Starting Date")
                {
                    ApplicationArea = All;
                    ToolTip = 'Specifies the starting date of the payment practice.';
                }
                field("Ending Date"; Rec."Ending Date")
                {
                    ApplicationArea = All;
                    ToolTip = 'Specifies the ending date of the payment practice.';
                }
                field("Generated On"; Rec."Generated On")
                {
                    ApplicationArea = All;
                    Editable = false;
                    ToolTip = 'Specifies the date and time when the payment practice was generated.';
                }
                field("Generated By"; Rec."Generated By")
                {
                    ApplicationArea = All;
                    Editable = false;
                    ToolTip = 'Specifies the user who generated the payment practice.';
                }
                field("Lines Exist"; Rec."Lines Exist")
                {
                    ApplicationArea = All;
                    Editable = false;
                    ToolTip = 'Specifies whether the payment practice has lines.';
                    Visible = false;
                }
                field("Modified Manually"; Rec."Modified Manually")
                {
                    ApplicationArea = All;
                    Editable = false;
                    ToolTip = 'Specifies whether the payment practice was modified manually.';
                }
            }
            group("Statistics")
            {
                field("Average Agreed Payment Period"; Rec."Average Agreed Payment Period")
                {
                    ApplicationArea = All;
                    ToolTip = 'Specifies the average agreed payment period.';

                    trigger OnDrillDown()
                    begin
                        ShowHeaderDataLines();
                    end;
                }
                field("Average Actual Payment Period"; Rec."Average Actual Payment Period")
                {
                    ApplicationArea = All;
                    ToolTip = 'Specifies the average actual payment period.';

                    trigger OnDrillDown()
                    begin
                        ShowHeaderDataLines();
                    end;
                }
                field("Pct Paid on Time"; Rec."Pct Paid on Time")
                {
                    ApplicationArea = All;
                    ToolTip = 'Specifies the percentage paid on time.';

                    trigger OnDrillDown()
                    begin
                        ShowHeaderDataLines();
                    end;
                }
            }
            part(Lines; "Payment Practice Lines")
            {
                ApplicationArea = All;
                SubPageLink = "Header No." = field("No.");
                UpdatePropagation = Both;
                Visible = Rec."Lines Exist";
            }
        }
    }

    actions
    {
        area(Processing)
        {
            action(Generate)
            {
                ApplicationArea = All;
                ToolTip = 'Generates the data and lines for the payment practice.';
                Image = CashFlow;

                trigger OnAction()
                var
                    PaymentPractices: Codeunit "Payment Practices";
                    ConfirmManagement: Codeunit "Confirm Management";
                begin
                    if Rec."Generated On" > 0DT then
                        if not ConfirmManagement.GetResponse(LinesWillBeDeletedQst, false) then
                            exit;

                    Rec.ClearHeader();
                    if not PaymentPractices.Generate(Rec) then
                        Message(NoEntriesFoundMsg);
                    UpdateVisibility();
                    CurrPage.Update();
                end;
            }
            action(Print)
            {
                ApplicationArea = All;
                ToolTip = 'Prints the payment practice report.';
                Image = Print;

                trigger OnAction()
                begin
                    PrepareLayout(Rec."Aggregation Type");
                    Rec.SetRecFilter();
                    Report.Run(Report::"Payment Practice", false, true, Rec);
                    FeatureTelemetry.LogUptake('0000KSV', 'Payment Practices', "Feature Uptake Status"::Used);
                end;
            }
        }
    }

    trigger OnOpenPage()
    begin
        CurrPage.Update();
        UpdateVisibility();
    end;

    var
        FeatureTelemetry: Codeunit "Feature Telemetry";
        LinesWillBeDeletedQst: Label 'All previously generated lines will be deleted. Do you want to continue?';
        NoEntriesFoundMsg: Label 'The payment practice generator found no entries were found corresponding to header type, starting and ending date.';

    local procedure PrepareLayout(PaymentPracticeLinesAggregator: Interface PaymentPracticeLinesAggregator)
    begin
        PaymentPracticeLinesAggregator.PrepareLayout();
    end;

    local procedure ShowHeaderDataLines()
    var
        PaymentPracticeData: Record "Payment Practice Data";
    begin
        PaymentPracticeData.SetRange("Header No.", Rec."No.");
        Page.RunModal(Page::"Payment Practice Data List", PaymentPracticeData);
    end;

    local procedure UpdateVisibility()
    begin
        CurrPage.Lines.Page.UpdateVisibility(Rec."Aggregation Type", Rec."Header Type");
        CurrPage.Update();
    end;
}