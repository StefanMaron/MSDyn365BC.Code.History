// ------------------------------------------------------------------------------------------------
// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License. See License.txt in the project root for license information.
// ------------------------------------------------------------------------------------------------
namespace Microsoft.eServices.EDocument.Test;

using Microsoft.eServices.EDocument;
using Microsoft.eServices.EDocument.Integration;
using Microsoft.eServices.EDocument.Processing.Import;
using Microsoft.eServices.EDocument.Processing.Import.Purchase;
using Microsoft.Finance.Currency;
using Microsoft.Finance.GeneralLedger.Setup;
using Microsoft.Finance.VAT.Setup;
using Microsoft.Foundation.Company;
using Microsoft.Purchases.History;
using Microsoft.Purchases.Payables;
using Microsoft.Purchases.Setup;
using Microsoft.Purchases.Vendor;
using Microsoft.Sales.Customer;
using System.IO;
using System.TestLibraries.Utilities;

codeunit 135576 "E-Doc Purch. VAT Tests"
{
    Subtype = Test;
    TestType = IntegrationTest;

    var
        Vendor: Record Vendor;
        Customer: Record Customer;
        EDocumentService: Record "E-Document Service";
        Assert: Codeunit Assert;
        LibraryVariableStorage: Codeunit "Library - Variable Storage";
        LibraryEDoc: Codeunit "Library - E-Document";
        EDocImplState: Codeunit "E-Doc. Impl. State";
        LibraryLowerPermission: Codeunit "Library - Lower Permissions";
        LibrarySetupStorage: Codeunit "Library - Setup Storage";
        IsInitialized: Boolean;

    [Test]
    procedure PreparingPurchaseDraftResolvesVATProductPostingGroupFromLineVATRate()
    var
        EDocument: Record "E-Document";
        EDocumentPurchaseHeader: Record "E-Document Purchase Header";
        EDocumentPurchaseLine: Record "E-Document Purchase Line";
        EDocImportParameters: Record "E-Doc. Import Parameters";
        Vendor2: Record Vendor;
        CompanyInformation: Record "Company Information";
        VATPostingSetup2: Record "VAT Posting Setup";
        VATProductPostingGroup: Record "VAT Product Posting Group";
        EDocumentProcessing: Codeunit "E-Document Processing";
        EDocImport: Codeunit "E-Doc. Import";
        LibraryERM: Codeunit "Library - ERM";
    begin
        // [SCENARIO] When a draft line has a VAT Rate and a matching VAT Posting Setup exists, Prepare Draft resolves the VAT Prod. Posting Group
        Initialize(Enum::"Service Integration"::"Mock");
        SetResolveVATProductGroupInPurchSetup(true);
        LibraryEDoc.CreateInboundEDocument(EDocument, EDocumentService);

        // [GIVEN] A vendor with a known VAT Bus. Posting Group
        CompanyInformation.GetRecordOnce();
        Vendor2."Country/Region Code" := CompanyInformation."Country/Region Code";
        Vendor2."No." := 'EDOC001';
        Vendor2."VAT Registration No." := 'XXXXXXX001';
        Vendor2."VAT Bus. Posting Group" := Vendor."VAT Bus. Posting Group";
        Vendor2.Insert();

        // [GIVEN] A VAT Posting Setup with VAT % = 10 for the vendor's bus posting group
        LibraryERM.CreateVATProductPostingGroup(VATProductPostingGroup);
        VATPostingSetup2."VAT Bus. Posting Group" := Vendor2."VAT Bus. Posting Group";
        VATPostingSetup2."VAT Prod. Posting Group" := VATProductPostingGroup.Code;
        VATPostingSetup2."VAT Calculation Type" := VATPostingSetup2."VAT Calculation Type"::"Normal VAT";
        VATPostingSetup2."VAT %" := 10;
        VATPostingSetup2."Sales VAT Account" := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2."Purchase VAT Account" := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2.Insert();

        // [GIVEN] E-Document purchase header and line with VAT Rate = 10
        EDocumentPurchaseHeader."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseHeader."Vendor VAT Id" := Vendor2."VAT Registration No.";
        EDocumentPurchaseHeader.Insert();
        EDocumentPurchaseLine."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseLine.Description := 'Test VAT resolution';
        EDocumentPurchaseLine."VAT Rate" := 10;
        EDocumentPurchaseLine.Insert();

        // [WHEN] Prepare Draft is run
        EDocumentProcessing.ModifyEDocumentProcessingStatus(EDocument, "Import E-Doc. Proc. Status"::"Ready for draft");
        EDocImportParameters."Step to Run" := "Import E-Document Steps"::"Prepare draft";
        EDocImport.ProcessIncomingEDocument(EDocument, EDocImportParameters);

        // [THEN] The VAT Prod. Posting Group is resolved from the matching setup
        EDocumentPurchaseLine.SetRecFilter();
        EDocumentPurchaseLine.FindFirst();
        Assert.AreEqual(VATProductPostingGroup.Code, EDocumentPurchaseLine."[BC] VAT Prod. Posting Group", 'The VAT Prod. Posting Group should be resolved from the matching VAT Posting Setup.');

        // Cleanup
        Vendor2.SetRecFilter();
        Vendor2.Delete();
        VATPostingSetup2.SetRecFilter();
        VATPostingSetup2.Delete();
        VATProductPostingGroup.SetRecFilter();
        VATProductPostingGroup.Delete();
    end;

    [Test]
    procedure PreparingPurchaseDraftSetsVATRateMismatchWhenNoMatchingVATSetup()
    var
        EDocument: Record "E-Document";
        EDocumentPurchaseHeader: Record "E-Document Purchase Header";
        EDocumentPurchaseLine: Record "E-Document Purchase Line";
        EDocImportParameters: Record "E-Doc. Import Parameters";
        Vendor2: Record Vendor;
        CompanyInformation: Record "Company Information";
        EDocumentProcessing: Codeunit "E-Document Processing";
        EDocImport: Codeunit "E-Doc. Import";
    begin
        // [SCENARIO] When a draft line has a VAT Rate but no matching VAT Posting Setup exists, Prepare Draft leaves the field blank and sets the mismatch flag
        Initialize(Enum::"Service Integration"::"Mock");
        SetResolveVATProductGroupInPurchSetup(true);
        LibraryEDoc.CreateInboundEDocument(EDocument, EDocumentService);

        // [GIVEN] A vendor with a known VAT Bus. Posting Group
        CompanyInformation.GetRecordOnce();
        Vendor2."Country/Region Code" := CompanyInformation."Country/Region Code";
        Vendor2."No." := 'EDOC001';
        Vendor2."VAT Registration No." := 'XXXXXXX001';
        Vendor2."VAT Bus. Posting Group" := Vendor."VAT Bus. Posting Group";
        Vendor2.Insert();

        // [GIVEN] E-Document purchase header and line with VAT Rate = 99 (no matching setup)
        EDocumentPurchaseHeader."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseHeader."Vendor VAT Id" := Vendor2."VAT Registration No.";
        EDocumentPurchaseHeader.Insert();
        EDocumentPurchaseLine."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseLine.Description := 'Test VAT mismatch';
        EDocumentPurchaseLine."VAT Rate" := 99;
        EDocumentPurchaseLine.Insert();

        // [WHEN] Prepare Draft is run
        EDocumentProcessing.ModifyEDocumentProcessingStatus(EDocument, "Import E-Doc. Proc. Status"::"Ready for draft");
        EDocImportParameters."Step to Run" := "Import E-Document Steps"::"Prepare draft";
        EDocImport.ProcessIncomingEDocument(EDocument, EDocImportParameters);

        // [THEN] The VAT Prod. Posting Group is blank and mismatch flag is set
        EDocumentPurchaseLine.SetRecFilter();
        EDocumentPurchaseLine.FindFirst();
        Assert.AreEqual('', EDocumentPurchaseLine."[BC] VAT Prod. Posting Group", 'The VAT Prod. Posting Group should be blank when no matching VAT Posting Setup exists.');

        // Cleanup
        Vendor2.SetRecFilter();
        Vendor2.Delete();
    end;

    [Test]
    procedure PreparingDraftIgnoresFullVATSetupWhenResolvingPostingGroup()
    var
        EDocument: Record "E-Document";
        EDocumentPurchaseHeader: Record "E-Document Purchase Header";
        EDocumentPurchaseLine: Record "E-Document Purchase Line";
        EDocImportParameters: Record "E-Doc. Import Parameters";
        Vendor2: Record Vendor;
        CompanyInformation: Record "Company Information";
        VATPostingSetup2: Record "VAT Posting Setup";
        VATProductPostingGroup: Record "VAT Product Posting Group";
        EDocumentProcessing: Codeunit "E-Document Processing";
        EDocImport: Codeunit "E-Doc. Import";
        LibraryERM: Codeunit "Library - ERM";
    begin
        // [SCENARIO] Full VAT setups must not be matched during VAT Posting Group resolution
        Initialize(Enum::"Service Integration"::"Mock");
        SetResolveVATProductGroupInPurchSetup(true);
        LibraryEDoc.CreateInboundEDocument(EDocument, EDocumentService);

        // [GIVEN] A vendor
        CompanyInformation.GetRecordOnce();
        Vendor2."Country/Region Code" := CompanyInformation."Country/Region Code";
        Vendor2."No." := 'EDOC001';
        Vendor2."VAT Registration No." := 'XXXXXXX001';
        Vendor2."VAT Bus. Posting Group" := Vendor."VAT Bus. Posting Group";
        Vendor2.Insert();

        // [GIVEN] A Full VAT Posting Setup with VAT % = 10
        LibraryERM.CreateVATProductPostingGroup(VATProductPostingGroup);
        VATPostingSetup2."VAT Bus. Posting Group" := Vendor2."VAT Bus. Posting Group";
        VATPostingSetup2."VAT Prod. Posting Group" := VATProductPostingGroup.Code;
        VATPostingSetup2."VAT Calculation Type" := VATPostingSetup2."VAT Calculation Type"::"Full VAT";
        VATPostingSetup2."VAT %" := 10;
        VATPostingSetup2."Sales VAT Account" := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2."Purchase VAT Account" := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2.Insert();

        // [GIVEN] E-Document line with VAT Rate = 10
        EDocumentPurchaseHeader."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseHeader."Vendor VAT Id" := Vendor2."VAT Registration No.";
        EDocumentPurchaseHeader.Insert();
        EDocumentPurchaseLine."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseLine.Description := 'Test Full VAT ignored';
        EDocumentPurchaseLine."VAT Rate" := 10;
        EDocumentPurchaseLine.Insert();

        // [WHEN] Prepare Draft is run
        EDocumentProcessing.ModifyEDocumentProcessingStatus(EDocument, "Import E-Doc. Proc. Status"::"Ready for draft");
        EDocImportParameters."Step to Run" := "Import E-Document Steps"::"Prepare draft";
        EDocImport.ProcessIncomingEDocument(EDocument, EDocImportParameters);

        // [THEN] Full VAT setup is not matched
        EDocumentPurchaseLine.SetRecFilter();
        EDocumentPurchaseLine.FindFirst();
        Assert.AreEqual('', EDocumentPurchaseLine."[BC] VAT Prod. Posting Group", 'Full VAT setups must not be matched.');

        // Cleanup
        Vendor2.SetRecFilter();
        Vendor2.Delete();
        VATPostingSetup2.SetRecFilter();
        VATPostingSetup2.Delete();
        VATProductPostingGroup.SetRecFilter();
        VATProductPostingGroup.Delete();
    end;

    [Test]
    procedure PreparingDraftIgnoresSalesTaxSetupWhenResolvingPostingGroup()
    var
        EDocument: Record "E-Document";
        EDocumentPurchaseHeader: Record "E-Document Purchase Header";
        EDocumentPurchaseLine: Record "E-Document Purchase Line";
        EDocImportParameters: Record "E-Doc. Import Parameters";
        Vendor2: Record Vendor;
        CompanyInformation: Record "Company Information";
        VATPostingSetup2: Record "VAT Posting Setup";
        VATProductPostingGroup: Record "VAT Product Posting Group";
        EDocumentProcessing: Codeunit "E-Document Processing";
        EDocImport: Codeunit "E-Doc. Import";
        LibraryERM: Codeunit "Library - ERM";
    begin
        // [SCENARIO] Sales Tax setups must not be matched during VAT Posting Group resolution
        Initialize(Enum::"Service Integration"::"Mock");
        SetResolveVATProductGroupInPurchSetup(true);
        LibraryEDoc.CreateInboundEDocument(EDocument, EDocumentService);

        // [GIVEN] A vendor
        CompanyInformation.GetRecordOnce();
        Vendor2."Country/Region Code" := CompanyInformation."Country/Region Code";
        Vendor2."No." := 'EDOC001';
        Vendor2."VAT Registration No." := 'XXXXXXX001';
        Vendor2."VAT Bus. Posting Group" := Vendor."VAT Bus. Posting Group";
        Vendor2.Insert();

        // [GIVEN] A Sales Tax Posting Setup with VAT % = 10
        LibraryERM.CreateVATProductPostingGroup(VATProductPostingGroup);
        VATPostingSetup2."VAT Bus. Posting Group" := Vendor2."VAT Bus. Posting Group";
        VATPostingSetup2."VAT Prod. Posting Group" := VATProductPostingGroup.Code;
        VATPostingSetup2."VAT Calculation Type" := VATPostingSetup2."VAT Calculation Type"::"Sales Tax";
        VATPostingSetup2."VAT %" := 10;
        VATPostingSetup2."Sales VAT Account" := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2."Purchase VAT Account" := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2.Insert();

        // [GIVEN] E-Document line with VAT Rate = 10
        EDocumentPurchaseHeader."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseHeader."Vendor VAT Id" := Vendor2."VAT Registration No.";
        EDocumentPurchaseHeader.Insert();
        EDocumentPurchaseLine."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseLine.Description := 'Test Sales Tax ignored';
        EDocumentPurchaseLine."VAT Rate" := 10;
        EDocumentPurchaseLine.Insert();

        // [WHEN] Prepare Draft is run
        EDocumentProcessing.ModifyEDocumentProcessingStatus(EDocument, "Import E-Doc. Proc. Status"::"Ready for draft");
        EDocImportParameters."Step to Run" := "Import E-Document Steps"::"Prepare draft";
        EDocImport.ProcessIncomingEDocument(EDocument, EDocImportParameters);

        // [THEN] Sales Tax setup is not matched
        EDocumentPurchaseLine.SetRecFilter();
        EDocumentPurchaseLine.FindFirst();
        Assert.AreEqual('', EDocumentPurchaseLine."[BC] VAT Prod. Posting Group", 'Sales Tax setups must not be matched.');

        // Cleanup
        Vendor2.SetRecFilter();
        Vendor2.Delete();
        VATPostingSetup2.SetRecFilter();
        VATPostingSetup2.Delete();
        VATProductPostingGroup.SetRecFilter();
        VATProductPostingGroup.Delete();
    end;

    [Test]
    procedure PreparingDraftResolvesReverseChargeVATPostingGroup()
    var
        EDocument: Record "E-Document";
        EDocumentPurchaseHeader: Record "E-Document Purchase Header";
        EDocumentPurchaseLine: Record "E-Document Purchase Line";
        EDocImportParameters: Record "E-Doc. Import Parameters";
        Vendor2: Record Vendor;
        CompanyInformation: Record "Company Information";
        VATPostingSetup2: Record "VAT Posting Setup";
        VATProductPostingGroup: Record "VAT Product Posting Group";
        EDocumentProcessing: Codeunit "E-Document Processing";
        EDocImport: Codeunit "E-Doc. Import";
        LibraryERM: Codeunit "Library - ERM";
    begin
        // [SCENARIO] Reverse Charge VAT setups should be matched during VAT Posting Group resolution
        Initialize(Enum::"Service Integration"::"Mock");
        SetResolveVATProductGroupInPurchSetup(true);
        LibraryEDoc.CreateInboundEDocument(EDocument, EDocumentService);

        // [GIVEN] A vendor
        CompanyInformation.GetRecordOnce();
        Vendor2."Country/Region Code" := CompanyInformation."Country/Region Code";
        Vendor2."No." := 'EDOC001';
        Vendor2."VAT Registration No." := 'XXXXXXX001';
        Vendor2."VAT Bus. Posting Group" := Vendor."VAT Bus. Posting Group";
        Vendor2.Insert();

        // [GIVEN] A Reverse Charge VAT Posting Setup with VAT % = 20
        LibraryERM.CreateVATProductPostingGroup(VATProductPostingGroup);
        VATPostingSetup2."VAT Bus. Posting Group" := Vendor2."VAT Bus. Posting Group";
        VATPostingSetup2."VAT Prod. Posting Group" := VATProductPostingGroup.Code;
        VATPostingSetup2."VAT Calculation Type" := VATPostingSetup2."VAT Calculation Type"::"Reverse Charge VAT";
        VATPostingSetup2."VAT %" := 20;
        VATPostingSetup2."Sales VAT Account" := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2."Purchase VAT Account" := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2."Reverse Chrg. VAT Acc." := LibraryERM.CreateGLAccountNo();
        VATPostingSetup2.Insert();

        // [GIVEN] E-Document line with VAT Rate = 20
        EDocumentPurchaseHeader."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseHeader."Vendor VAT Id" := Vendor2."VAT Registration No.";
        EDocumentPurchaseHeader.Insert();
        EDocumentPurchaseLine."E-Document Entry No." := EDocument."Entry No";
        EDocumentPurchaseLine.Description := 'Test Reverse Charge resolved';
        EDocumentPurchaseLine."VAT Rate" := 20;
        EDocumentPurchaseLine.Insert();

        // [WHEN] Prepare Draft is run
        EDocumentProcessing.ModifyEDocumentProcessingStatus(EDocument, "Import E-Doc. Proc. Status"::"Ready for draft");
        EDocImportParameters."Step to Run" := "Import E-Document Steps"::"Prepare draft";
        EDocImport.ProcessIncomingEDocument(EDocument, EDocImportParameters);

        // [THEN] Reverse Charge VAT setup is matched
        EDocumentPurchaseLine.SetRecFilter();
        EDocumentPurchaseLine.FindFirst();
        Assert.AreEqual(VATProductPostingGroup.Code, EDocumentPurchaseLine."[BC] VAT Prod. Posting Group", 'Reverse Charge VAT setups should be matched.');

        // Cleanup
        Vendor2.SetRecFilter();
        Vendor2.Delete();
        VATPostingSetup2.SetRecFilter();
        VATPostingSetup2.Delete();
        VATProductPostingGroup.SetRecFilter();
        VATProductPostingGroup.Delete();
    end;

    local procedure Initialize(Integration: Enum "Service Integration")
    var
        TransformationRule: Record "Transformation Rule";
        EDocument: Record "E-Document";
        EDocDataStorage: Record "E-Doc. Data Storage";
        EDocumentServiceStatus: Record "E-Document Service Status";
        EDocPurchLineFieldSetup: Record "ED Purchase Line Field Setup";
        PurchInvHeader: Record "Purch. Inv. Header";
        VendorLedgerEntry: Record "Vendor Ledger Entry";
        GLSetup: Record "General Ledger Setup";
        Currency: Record Currency;
        LibraryERM: Codeunit "Library - ERM";
    begin
        LibrarySetupStorage.Restore();
        LibraryLowerPermission.SetOutsideO365Scope();
        LibraryVariableStorage.Clear();
        Clear(EDocImplState);
        EDocPurchLineFieldSetup.DeleteAll();

        PurchInvHeader.DeleteAll();
        VendorLedgerEntry.DeleteAll();

        if IsInitialized then
            exit;

        GLSetup.GetRecordOnce();
        GLSetup."VAT Reporting Date Usage" := GLSetup."VAT Reporting Date Usage"::Disabled;
        GLSetup.Modify();

        // Set a currency that can be used across all localizations
        Currency.Init();
        Currency.Validate(Code, 'XYZ');
        if Currency.Insert(true) then
            LibraryERM.CreateExchangeRate(Currency.Code, WorkDate(), 1.0, 1.0);

        EDocument.DeleteAll();
        EDocumentServiceStatus.DeleteAll();
        EDocumentService.DeleteAll();
        EDocDataStorage.DeleteAll();

        LibraryEDoc.SetupStandardVAT();
        LibraryEDoc.SetupStandardSalesScenario(Customer, EDocumentService, Enum::"E-Document Format"::Mock, Integration);
        LibraryEDoc.SetupStandardPurchaseScenario(Vendor, EDocumentService, Enum::"E-Document Format"::Mock, Integration);
        EDocumentService."Import Process" := "E-Document Import Process"::"Version 2.0";
        EDocumentService."Read into Draft Impl." := "E-Doc. Read into Draft"::PEPPOL;
        EDocumentService.Modify();

        TransformationRule.DeleteAll();
        TransformationRule.CreateDefaultTransformations();
        LibrarySetupStorage.SavePurchasesSetup();

        IsInitialized := true;
    end;

    local procedure SetResolveVATProductGroupInPurchSetup(NewResolveVATProductGroup: Boolean)
    var
        PurchasesPayablesSetup: Record "Purchases & Payables Setup";
    begin
        PurchasesPayablesSetup.Get();
        PurchasesPayablesSetup.Validate("Resolve VAT Group Purch EDoc", NewResolveVATProductGroup);
        PurchasesPayablesSetup.Modify();
    end;
}
