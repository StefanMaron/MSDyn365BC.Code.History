namespace Microsoft.Sustainability.Purchase;

using Microsoft.Finance.GeneralLedger.Account;
using Microsoft.Finance.GeneralLedger.Journal;
using Microsoft.Finance.GeneralLedger.Preview;
using Microsoft.Finance.ReceivablesPayables;
using Microsoft.FixedAssets.FixedAsset;
using Microsoft.Inventory;
using Microsoft.Inventory.Costing;
using Microsoft.Inventory.Item;
using Microsoft.Inventory.Journal;
using Microsoft.Inventory.Ledger;
using Microsoft.Inventory.Tracking;
using Microsoft.Inventory.Transfer;
using Microsoft.Projects.Resources.Resource;
using Microsoft.Purchases.Document;
using Microsoft.Purchases.History;
using Microsoft.Purchases.Posting;
using Microsoft.Sales.History;
using Microsoft.Sustainability.Account;
using Microsoft.Sustainability.Calculation;
using Microsoft.Sustainability.Journal;
using Microsoft.Sustainability.Posting;
using Microsoft.Sustainability.Setup;

codeunit 6225 "Sust. Purchase Subscriber"
{
    [EventSubscriber(ObjectType::Table, Database::"Purchase Line", 'OnValidateQuantityOnBeforeResetAmounts', '', false, false)]
    local procedure OnValidateQuantityOnBeforeResetAmounts(var PurchaseLine: Record "Purchase Line")
    var
        SustainabilityCalcMgt: Codeunit "Sustainability Calc. Mgt.";
    begin
        if PurchaseLine.IsSustainabilityFormulaPurchaseLine() then
            SustainabilityCalcMgt.CalculationEmissions(PurchaseLine)
        else
            PurchaseLine.UpdateSustainabilityEmission(PurchaseLine);

        PurchaseLine.UpdateCarbonPricingInPurchLine(PurchaseLine);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnAfterPostPurchLine', '', false, false)]
    local procedure OnAfterPostPurchLine(var PurchaseHeader: Record "Purchase Header"; var PurchaseLine: Record "Purchase Line"; SrcCode: Code[10]; GenJnlLineDocNo: Code[20])
    begin
        if (PurchaseHeader.Invoice) and (PurchaseLine."Qty. to Invoice" <> 0) then
            PostSustainabilityLine(PurchaseHeader, PurchaseLine, SrcCode, GenJnlLineDocNo);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnPostUpdateOrderLineOnBeforeUpdateBlanketOrderLine', '', false, false)]
    local procedure OnPostUpdateOrderLineOnBeforeUpdateBlanketOrderLine(var PurchaseHeader: Record "Purchase Header"; var TempPurchaseLine: Record "Purchase Line" temporary)
    begin
        if PurchaseHeader.Invoice then
            UpdatePostedSustainabilityEmissionOrderLine(PurchaseHeader, TempPurchaseLine);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnInsertReceiptLineOnAfterInitPurchRcptLine', '', false, false)]
    local procedure OnInsertReceiptLineOnAfterInitPurchRcptLine(PurchLine: Record "Purchase Line"; var PurchRcptLine: Record "Purch. Rcpt. Line")
    begin
        UpdatePostedSustainabilityEmission(PurchLine, PurchRcptLine.Quantity, 1, PurchRcptLine."Emission CO2", PurchRcptLine."Emission CH4", PurchRcptLine."Emission N2O", PurchRcptLine."Energy Consumption", PurchRcptLine."Total Emission Cost");
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnInsertReturnShipmentLineOnAfterReturnShptLineInit', '', false, false)]
    local procedure OnInsertReturnShipmentLineOnAfterReturnShptLineInit(PurchLine: Record "Purchase Line"; var ReturnShptLine: Record "Return Shipment Line")
    begin
        UpdatePostedSustainabilityEmission(PurchLine, ReturnShptLine.Quantity, 1, ReturnShptLine."Emission CO2", ReturnShptLine."Emission CH4", ReturnShptLine."Emission N2O", ReturnShptLine."Energy Consumption", ReturnShptLine."Total Emission Cost");
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnBeforePurchInvLineInsert', '', false, false)]
    local procedure OnBeforePurchInvLineInsert(var PurchaseLine: Record "Purchase Line"; var PurchInvLine: Record "Purch. Inv. Line")
    begin
        UpdatePostedSustainabilityEmission(PurchaseLine, PurchInvLine.Quantity, 1, PurchInvLine."Emission CO2", PurchInvLine."Emission CH4", PurchInvLine."Emission N2O", PurchInvLine."Energy Consumption", PurchInvLine."Total Emission Cost");
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnBeforePurchCrMemoLineInsert', '', false, false)]
    local procedure OnBeforePurchCrMemoLineInsert(PurchLine: Record "Purchase Line"; var PurchCrMemoLine: Record "Purch. Cr. Memo Line")
    begin
        UpdatePostedSustainabilityEmission(PurchLine, PurchCrMemoLine.Quantity, 1, PurchCrMemoLine."Emission CO2", PurchCrMemoLine."Emission CH4", PurchCrMemoLine."Emission N2O", PurchCrMemoLine."Energy Consumption", PurchCrMemoLine."Total Emission Cost");
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Gen. Jnl.-Post Preview", 'OnAfterBindSubscription', '', false, false)]
    local procedure OnAfterBindSubscription()
    begin
        TryBindPostingPreviewHandler();
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Gen. Jnl.-Post Preview", 'OnAfterUnbindSubscription', '', false, false)]
    local procedure OnAfterUnbindSubscription()
    begin
        TryUnbindPostingPreviewHandler();
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnBeforeItemJnlPostLine', '', false, false)]
    local procedure OnBeforeItemJnlPostLine(var ItemJournalLine: Record "Item Journal Line"; PurchaseHeader: Record "Purchase Header"; PurchaseLine: Record "Purchase Line"; TempItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)" temporary)
    var
        DistributeItemChargeCO2e: Boolean;
    begin
        if (ItemJournalLine.Quantity = 0) and (ItemJournalLine."Invoiced Quantity" = 0) then
            exit;

        if PurchaseLine."Sust. Account No." <> '' then
            if PurchaseLine.Type = PurchaseLine.Type::"Charge (Item)" then
                DistributeItemChargeCO2e := GetDistributeItemCharge(TempItemChargeAssignmentPurch);

        CheckAndUpdateSustainabilityItemJournalLine(ItemJournalLine, PurchaseHeader, PurchaseLine, TempItemChargeAssignmentPurch, DistributeItemChargeCO2e);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Undo Purchase Receipt Line", 'OnAfterCopyItemJnlLineFromPurchRcpt', '', false, false)]
    local procedure OnAfterCopyItemJnlLineFromPurchRcptForUndoPurchaseReceipt(var ItemJournalLine: Record "Item Journal Line"; var PurchRcptLine: Record "Purch. Rcpt. Line")
    begin
        if (ItemJournalLine.Quantity <> 0) or (ItemJournalLine."Invoiced Quantity" <> 0) then
            UpdateSustainabilityItemJournalLine(ItemJournalLine, PurchRcptLine);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Undo Posting Management", 'OnPostItemJnlLineAppliedToListOnAfterSetInvoicedQty', '', false, false)]
    local procedure OnPostItemJnlLineAppliedToListOnAfterSetInvoicedQty(var ItemJournalLine: Record "Item Journal Line"; TempApplyToItemLedgEntry: Record "Item Ledger Entry" temporary)
    var
        PurchReceiptLine: Record "Purch. Rcpt. Line";
    begin
        if (ItemJournalLine."Quantity (Base)" <> 0) or (ItemJournalLine."Invoiced Qty. (Base)" <> 0) then
            if ItemJournalLine."Document Type" = ItemJournalLine."Document Type"::"Purchase Receipt" then
                if PurchReceiptLine.Get(ItemJournalLine."Document No.", TempApplyToItemLedgEntry."Document Line No.") then
                    UpdateSustainabilityItemJournalLine(ItemJournalLine, PurchReceiptLine);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnPostItemChargePerOrderOnAfterCopyToItemJnlLine', '', false, false)]
    local procedure OnPostItemChargePerOrderOnAfterCopyToItemJnlLine(var ItemJournalLine: Record "Item Journal Line"; PurchaseLine: Record "Purchase Line"; TempItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)" temporary; PurchLine: Record "Purchase Line")
    var
        PurchaseHeader: Record "Purchase Header";
    begin
        PurchaseHeader.Get(PurchaseLine."Document Type", PurchaseLine."Document No.");
        if (ItemJournalLine.Quantity <> 0) or (ItemJournalLine."Invoiced Quantity" <> 0) then
            CheckAndUpdateSustainabilityItemJournalLine(ItemJournalLine, PurchaseHeader, PurchaseLine, TempItemChargeAssignmentPurch, false);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnPostItemTrackingItemChargePerOrderOnAfterCalcFactor', '', false, false)]
    local procedure OnPostItemTrackingItemChargePerOrderOnAfterCalcFactor(var NonDistrItemJnlLine: Record "Item Journal Line"; var ItemJnlLine2: Record "Item Journal Line"; var TempTrackingSpecificationChargeAssmt: Record "Tracking Specification"; SignFactor: Integer; Factor: Decimal)
    var
        CO2eToAssign: Decimal;
    begin
        if ItemJnlLine2."Sust. Account No." = '' then
            exit;

        if NonDistrItemJnlLine."Quantity (Base)" = 0 then
            exit;

        if Abs(TempTrackingSpecificationChargeAssmt."Quantity (Base)") < Abs(NonDistrItemJnlLine."Quantity (Base)") then begin
            CO2eToAssign := NonDistrItemJnlLine."Total CO2e" * TempTrackingSpecificationChargeAssmt."Quantity (Base)" / NonDistrItemJnlLine."Quantity (Base)";
            ItemJnlLine2."Total CO2e" := CO2eToAssign;
            NonDistrItemJnlLine."Total CO2e" -= CO2eToAssign;
        end;
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnPostItemJnlLineJobConsumptionOnBeforeRunItemJnlPostLineWithReservation', '', false, false)]
    local procedure OnPostItemJnlLineJobConsumption(var ItemJournalLine: Record "Item Journal Line"; var PurchaseLine: Record "Purchase Line")
    var
        PurchHeader: Record "Purchase Header";
        TempItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)" temporary;
    begin
        PurchHeader.Get(PurchaseLine."Document Type", PurchaseLine."Document No.");
        if ((ItemJournalLine.Quantity <> 0) or (ItemJournalLine."Invoiced Quantity" <> 0)) then
            CheckAndUpdateSustainabilityItemJournalLine(ItemJournalLine, PurchHeader, PurchaseLine, TempItemChargeAssignmentPurch, false);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch. Post Invoice Events", 'OnAfterPrepareInvoicePostingBuffer', '', false, false)]
    local procedure OnAfterPrepareInvoicePostingBuffer(var InvoicePostingBuffer: Record "Invoice Posting Buffer" temporary; var PurchaseLine: Record "Purchase Line")
    begin
        if (PurchaseLine."Qty. to Invoice" <> 0) and (PurchaseLine.Type = PurchaseLine.Type::"Fixed Asset") then
            CheckAndUpdateSustainabilityInvoicePostingBuffer(InvoicePostingBuffer, PurchaseLine);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch. Post Invoice Events", 'OnAfterPrepareGenJnlLine', '', false, false)]
    local procedure OnAfterPrepareGenJnlLine(InvoicePostingBuffer: Record "Invoice Posting Buffer" temporary; var GenJnlLine: Record "Gen. Journal Line")
    begin
        GenJnlLine."Sust. Account No." := InvoicePostingBuffer."Sust. Account No.";
        GenJnlLine."Sust. Account Name" := InvoicePostingBuffer."Sust. Account Name";
        GenJnlLine."Sust. Account Category" := InvoicePostingBuffer."Sust. Account Category";
        GenJnlLine."Sust. Account Subcategory" := InvoicePostingBuffer."Sust. Account Subcategory";
        GenJnlLine."CO2e per Unit" := InvoicePostingBuffer."CO2e per Unit";
        GenJnlLine."Total CO2e" := InvoicePostingBuffer."Total CO2e";
    end;

    [EventSubscriber(ObjectType::Table, Database::"Purchase Line", 'OnNotHandledCopyFromGLAccount', '', false, false)]
    local procedure OnAfterAssignGLAccountValues(var PurchaseLine: Record "Purchase Line"; GLAccount: Record "G/L Account")
    begin
        PurchaseLine.Validate("Sust. Account No.", GLAccount."Default Sust. Account");
    end;

    [EventSubscriber(ObjectType::Table, Database::"Purchase Line", 'OnAfterAssignItemValues', '', false, false)]
    local procedure OnAfterAssignItemValues(var PurchLine: Record "Purchase Line"; Item: Record Item)
    begin
        PurchLine.Validate("Sust. Account No.", Item."Default Sust. Account");
        PurchLine.Validate("EUDR Relevant", Item."EUDR Relevant");
    end;

    [EventSubscriber(ObjectType::Table, Database::"Purchase Line", 'OnAfterAssignResourceValues', '', false, false)]
    local procedure OnAfterAssignResourceValues(var PurchaseLine: Record "Purchase Line"; Resource: Record Resource)
    begin
        PurchaseLine.Validate("Sust. Account No.", Resource."Default Sust. Account");
    end;

    [EventSubscriber(ObjectType::Table, Database::"Purchase Line", 'OnAfterAssignItemChargeValues', '', false, false)]
    local procedure OnAfterAssignItemChargeValues(var PurchLine: Record "Purchase Line"; ItemCharge: Record "Item Charge")
    begin
        PurchLine.Validate("Sust. Account No.", ItemCharge."Default Sust. Account");
    end;

    [EventSubscriber(ObjectType::Table, Database::"Purchase Line", 'OnAfterAssignFixedAssetValues', '', false, false)]
    local procedure OnAfterAssignFixedAssetValues(var PurchLine: Record "Purchase Line"; FixedAsset: Record "Fixed Asset")
    begin
        PurchLine.Validate("Sust. Account No.", FixedAsset."Default Sust. Account");
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Item Charge Assgnt. (Purch.)", 'OnBeforeInsertItemChargeAssgntWithAssignValues', '', false, false)]
    local procedure OnBeforeInsertItemChargeAssgntWithAssignValues(FromItemChargeAssgntPurch: Record "Item Charge Assignment (Purch)"; var ItemChargeAssgntPurch: Record "Item Charge Assignment (Purch)")
    var
        PurchaseLine: Record "Purchase Line";
    begin
        if PurchaseLine.Get(ItemChargeAssgntPurch."Document Type", ItemChargeAssgntPurch."Document No.", ItemChargeAssgntPurch."Document Line No.") then
            CheckAndUpdateSustainabilityItemChargeAssignmentPurch(ItemChargeAssgntPurch, PurchaseLine);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Item Charge Assgnt. (Purch.)", 'OnAssignItemChargesFromLineOnAfterItemChargeAssignmentModifyAll', '', false, false)]
    local procedure OnAssignItemChargesFromLineOnAfterItemChargeAssignmentModifyAll(var ItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)")
    begin
        ItemChargeAssignmentPurch.ModifyAll("CO2e to Assign", 0);
        ItemChargeAssignmentPurch.ModifyAll("CO2e to Handle", 0);
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Item Charge Assgnt. (Purch.)", 'OnSuggestAssgntFromLineOnBeforeItemChargeAssignmentPurchModify', '', false, false)]
    local procedure OnSuggestAssgntFromLineOnBeforeItemChargeAssignmentPurchModify(var ItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)")
    begin
        ItemChargeAssignmentPurch."CO2e to Assign" := ItemChargeAssignmentPurch."Qty. to Assign" * ItemChargeAssignmentPurch."CO2e per Unit";
        ItemChargeAssignmentPurch."CO2e to Handle" := ItemChargeAssignmentPurch."Qty. to Handle" * ItemChargeAssignmentPurch."CO2e per Unit";
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Item Charge Assgnt. (Purch.)", 'OnAssignEquallyOnBeforeItemChargeAssignmentPurchModify', '', false, false)]
    local procedure OnAssignEquallyOnBeforeItemChargeAssignmentPurchModify(var ItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)")
    begin
        ItemChargeAssignmentPurch."CO2e to Assign" := ItemChargeAssignmentPurch."Qty. to Assign" * ItemChargeAssignmentPurch."CO2e per Unit";
        ItemChargeAssignmentPurch."CO2e to Handle" := ItemChargeAssignmentPurch."Qty. to Handle" * ItemChargeAssignmentPurch."CO2e per Unit";
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Item Charge Assgnt. (Purch.)", 'OnAssignByAmountOnBeforeItemChargeAssignmentPurchModify', '', false, false)]
    local procedure OnAssignByAmountOnBeforeItemChargeAssignmentPurchModify(var ItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)")
    begin
        ItemChargeAssignmentPurch."CO2e to Assign" := ItemChargeAssignmentPurch."Qty. to Assign" * ItemChargeAssignmentPurch."CO2e per Unit";
        ItemChargeAssignmentPurch."CO2e to Handle" := ItemChargeAssignmentPurch."Qty. to Handle" * ItemChargeAssignmentPurch."CO2e per Unit";
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Item Charge Assgnt. (Purch.)", 'OnAssignPurchItemChargeOnBeforeItemChargeAssignmentPurchModify', '', false, false)]
    local procedure OnAssignPurchItemChargeOnBeforeItemChargeAssignmentPurchModify(var ItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)")
    begin
        ItemChargeAssignmentPurch."CO2e to Assign" := ItemChargeAssignmentPurch."Qty. to Assign" * ItemChargeAssignmentPurch."CO2e per Unit";
        ItemChargeAssignmentPurch."CO2e to Handle" := ItemChargeAssignmentPurch."Qty. to Handle" * ItemChargeAssignmentPurch."CO2e per Unit";
    end;

    [EventSubscriber(ObjectType::Codeunit, Codeunit::"Purch.-Post", 'OnUpdateItemChargeAssgntOnBeforeItemChargeAssignmentPurchModify', '', false, false)]
    local procedure OnUpdateItemChargeAssgntOnBeforeItemChargeAssignmentPurchModifyPurchPost(var ItemChargeAssgntPurch: Record "Item Charge Assignment (Purch)")
    begin
        ItemChargeAssgntPurch."CO2e to Assign" -= ItemChargeAssgntPurch."CO2e to Handle";
        ItemChargeAssgntPurch."CO2e to Handle" := 0;
    end;

    [EventSubscriber(ObjectType::Table, Database::"Purchase Line", 'OnUpdateItemChargeAssgntOnBeforeItemChargeAssignmentPurchModify', '', false, false)]
    local procedure OnUpdateItemChargeAssgntOnBeforeItemChargeAssignmentPurchModify(var PurchaseLine: Record "Purchase Line"; var ItemChargeAssignmentPurch: Record "Item Charge Assignment (Purch)")
    begin
        CheckAndUpdateSustainabilityItemChargeAssignmentPurch(ItemChargeAssignmentPurch, PurchaseLine);
    end;

    internal procedure GetCO2eEmissionFromPurchLine(var PurchaseLine: Record "Purchase Line"; var CO2eEmission: Decimal)
    var
        PurchaseHeader: Record "Purchase Header";
        SustainabilityPostMgt: Codeunit "Sustainability Post Mgt";
        GHGCredit: Boolean;
        CO2ToPost: Decimal;
        CH4ToPost: Decimal;
        N2OToPost: Decimal;
        CarbonFee: Decimal;
    begin
        PurchaseHeader := PurchaseLine.GetPurchHeader();
        GHGCredit := IsGHGCreditLine(PurchaseLine);

        if GHGCredit then begin
            PurchaseLine.TestField("Emission CH4 Per Unit", 0);
            PurchaseLine.TestField("Emission N2O Per Unit", 0);
        end;

        if PurchaseLine.Type = PurchaseLine.Type::"Charge (Item)" then begin
            CO2ToPost := PurchaseLine."Emission CO2 Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";
            CH4ToPost := PurchaseLine."Emission CH4 Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";
            N2OToPost := PurchaseLine."Emission N2O Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";
        end else begin
            CO2ToPost := PurchaseLine."Emission CO2 Per Unit" * Abs(PurchaseLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
            CH4ToPost := PurchaseLine."Emission CH4 Per Unit" * Abs(PurchaseLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
            N2OToPost := PurchaseLine."Emission N2O Per Unit" * Abs(PurchaseLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
        end;
        if not SustainabilitySetup.IsValueChainTrackingEnabled() then
            exit;

        SustainabilityPostMgt.UpdateCarbonFeeEmissionValues("Emission Scope"::" ", PurchaseHeader."Posting Date", PurchaseHeader."Buy-from Country/Region Code", CO2ToPost, N2OToPost, CH4ToPost, CO2eEmission, CarbonFee);
    end;

    local procedure CheckAndUpdateSustainabilityItemChargeAssignmentPurch(var ItemChargeAssgntPurch: Record "Item Charge Assignment (Purch)"; PurchaseLine: Record "Purchase Line")
    var
        PurchaseHeader: Record "Purchase Header";
        SustainabilityPostMgt: Codeunit "Sustainability Post Mgt";
        GHGCredit: Boolean;
        CO2ToPost: Decimal;
        CH4ToPost: Decimal;
        N2OToPost: Decimal;
        CO2eEmission: Decimal;
        CarbonFee: Decimal;
        Denominator: Decimal;
    begin
        PurchaseHeader.Get(ItemChargeAssgntPurch."Document Type", ItemChargeAssgntPurch."Document No.");
        GHGCredit := IsGHGCreditLine(PurchaseLine);

        if GHGCredit then begin
            PurchaseLine.TestField("Emission CH4 Per Unit", 0);
            PurchaseLine.TestField("Emission N2O Per Unit", 0);
        end;

        ItemChargeAssgntPurch."CO2e per Unit" := 0;
        ItemChargeAssgntPurch."CO2e to Assign" := 0;
        ItemChargeAssgntPurch."CO2e to Handle" := 0;
        CO2ToPost := PurchaseLine."Emission CO2 Per Unit" * Abs(PurchaseLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
        CH4ToPost := PurchaseLine."Emission CH4 Per Unit" * Abs(PurchaseLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
        N2OToPost := PurchaseLine."Emission N2O Per Unit" * Abs(PurchaseLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
        if not SustainabilitySetup.IsValueChainTrackingEnabled() then
            exit;

        if PurchaseLine."Sust. Account No." = '' then
            exit;

        SustainabilityPostMgt.UpdateCarbonFeeEmissionValues("Emission Scope"::" ", PurchaseHeader."Posting Date", PurchaseHeader."Buy-from Country/Region Code", CO2ToPost, N2OToPost, CH4ToPost, CO2eEmission, CarbonFee);
        Denominator := PurchaseLine."Qty. per Unit of Measure" * PurchaseLine.Quantity;

        ItemChargeAssgntPurch."CO2e per Unit" := CO2eEmission / Denominator;
        ItemChargeAssgntPurch."CO2e to Assign" := ItemChargeAssgntPurch."Qty. to Assign" * ItemChargeAssgntPurch."CO2e per Unit";
        ItemChargeAssgntPurch."CO2e to Handle" := ItemChargeAssgntPurch."Qty. to Handle" * ItemChargeAssgntPurch."CO2e per Unit";
    end;

    local procedure CheckAndUpdateSustainabilityItemJournalLine(var ItemJournalLine: Record "Item Journal Line"; PurchaseHeader: Record "Purchase Header"; var PurchaseLine: Record "Purchase Line"; var TempItemChargeAssgntPurch: Record "Item Charge Assignment (Purch)" temporary; DistributeItemChargeCO2e: Boolean)
    var
        SustainabilityPostMgt: Codeunit "Sustainability Post Mgt";
        GHGCredit: Boolean;
        Sign: Integer;
        CO2ToPost: Decimal;
        CH4ToPost: Decimal;
        N2OToPost: Decimal;
        CarbonFee: Decimal;
        ChargeQtyToPostBase: Decimal;
        ItemChargeTotalQty: Decimal;
    begin
        GHGCredit := IsGHGCreditLine(PurchaseLine);

        if GHGCredit then begin
            PurchaseLine.TestField("Emission CH4 Per Unit", 0);
            PurchaseLine.TestField("Emission N2O Per Unit", 0);
        end;

        Sign := GetPostingSign(PurchaseHeader, GHGCredit);

        if (ItemJournalLine."Job No." <> '') and (ItemJournalLine."Entry Type" = ItemJournalLine."Entry Type"::"Negative Adjmt.") then
            Sign := -Sign;

        if ItemJournalLine."Invoiced Quantity" <> 0 then begin
            CO2ToPost := PurchaseLine."Emission CO2 Per Unit" * Abs(ItemJournalLine."Invoiced Quantity") * PurchaseLine."Qty. per Unit of Measure";
            CH4ToPost := PurchaseLine."Emission CH4 Per Unit" * Abs(ItemJournalLine."Invoiced Quantity") * PurchaseLine."Qty. per Unit of Measure";
            N2OToPost := PurchaseLine."Emission N2O Per Unit" * Abs(ItemJournalLine."Invoiced Quantity") * PurchaseLine."Qty. per Unit of Measure";
        end else begin
            CO2ToPost := PurchaseLine."Emission CO2 Per Unit" * Abs(ItemJournalLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
            CH4ToPost := PurchaseLine."Emission CH4 Per Unit" * Abs(ItemJournalLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
            N2OToPost := PurchaseLine."Emission N2O Per Unit" * Abs(ItemJournalLine.Quantity) * PurchaseLine."Qty. per Unit of Measure";
        end;

        CO2ToPost := CO2ToPost * Sign;
        CH4ToPost := CH4ToPost * Sign;
        N2OToPost := N2OToPost * Sign;

        if not SustainabilitySetup.IsValueChainTrackingEnabled() then
            exit;

        if not CanPostSustainabilityJnlLine(PurchaseLine, CO2ToPost, CH4ToPost, N2OToPost, 0, false) then
            exit;

        ItemJournalLine."Sust. Account No." := PurchaseLine."Sust. Account No.";
        ItemJournalLine."Sust. Account Name" := PurchaseLine."Sust. Account Name";
        ItemJournalLine."Sust. Account Category" := PurchaseLine."Sust. Account Category";
        ItemJournalLine."Sust. Account Subcategory" := PurchaseLine."Sust. Account Subcategory";
        if PurchaseLine.Type = PurchaseLine.Type::"Charge (Item)" then begin

            if not DistributeItemChargeCO2e then begin
                ItemJournalLine."Total CO2e" := Sign * TempItemChargeAssgntPurch."CO2e to Assign";
                exit;
            end;

            if PurchaseLine."Document Type" in [PurchaseLine."Document Type"::Order, PurchaseLine."Document Type"::Invoice] then
                ItemChargeTotalQty := Abs(TempItemChargeAssgntPurch."Qty. to Assign")
            else
                ItemChargeTotalQty := Abs(PurchaseLine."Qty. to Invoice");

            if ItemJournalLine."Invoiced Qty. (Base)" <> 0 then
                ChargeQtyToPostBase := Abs(ItemJournalLine."Invoiced Qty. (Base)")
            else
                ChargeQtyToPostBase := Abs(ItemJournalLine."Quantity (Base)");

            if (ChargeQtyToPostBase <> 0) and (Abs(ItemChargeTotalQty) <> 0) then
                ItemJournalLine."Total CO2e" := (Sign * TempItemChargeAssgntPurch."CO2e to Assign" * ChargeQtyToPostBase) / ItemChargeTotalQty
            else
                ItemJournalLine."Total CO2e" := Sign * TempItemChargeAssgntPurch."CO2e to Assign";
        end else begin
            ItemJournalLine."Emission CO2" := CO2ToPost;
            ItemJournalLine."Emission CH4" := CH4ToPost;
            ItemJournalLine."Emission N2O" := N2OToPost;
            if (ItemJournalLine."Job No." <> '') and (ItemJournalLine."Entry Type" = ItemJournalLine."Entry Type"::"Negative Adjmt.") then
                SustainabilityPostMgt.UpdateCarbonFeeEmissionValues("Emission Scope"::" ", PurchaseHeader."Posting Date", PurchaseHeader."Buy-from Country/Region Code", CO2ToPost, N2OToPost, CH4ToPost, ItemJournalLine."Total CO2e", CarbonFee);
        end;
    end;

    local procedure GetDistributeItemCharge(TempItemChargeAssgntPurch: Record "Item Charge Assignment (Purch)" temporary): Boolean
    var
        PurchRcptLine: Record "Purch. Rcpt. Line";
        ReturnShptLine: Record "Return Shipment Line";
        SalesShptLine: Record "Sales Shipment Line";
        ReturnRcptLine: Record "Return Receipt Line";
        TransRcptLine: Record "Transfer Receipt Line";
        TempItemLedgEntry: Record "Item Ledger Entry" temporary;
        CostCalcMgt: Codeunit "Cost Calculation Management";
    begin
        case TempItemChargeAssgntPurch."Applies-to Doc. Type" of
            TempItemChargeAssgntPurch."Applies-to Doc. Type"::Receipt:
                if PurchRcptLine.Get(TempItemChargeAssgntPurch."Applies-to Doc. No.", TempItemChargeAssgntPurch."Applies-to Doc. Line No.") then begin
                    if PurchRcptLine."Item Rcpt. Entry No." <> 0 then
                        exit(CostCalcMgt.SplitItemLedgerEntriesExist(TempItemLedgEntry, PurchRcptLine."Quantity (Base)", PurchRcptLine."Item Rcpt. Entry No."));
                    exit(true);
                end;
            TempItemChargeAssgntPurch."Applies-to Doc. Type"::"Return Shipment":
                if ReturnShptLine.Get(TempItemChargeAssgntPurch."Applies-to Doc. No.", TempItemChargeAssgntPurch."Applies-to Doc. Line No.") then begin
                    if ReturnShptLine."Item Shpt. Entry No." <> 0 then
                        exit(CostCalcMgt.SplitItemLedgerEntriesExist(TempItemLedgEntry, -ReturnShptLine."Quantity (Base)", ReturnShptLine."Item Shpt. Entry No."));
                    exit(true);
                end;
            TempItemChargeAssgntPurch."Applies-to Doc. Type"::"Sales Shipment":
                if SalesShptLine.Get(TempItemChargeAssgntPurch."Applies-to Doc. No.", TempItemChargeAssgntPurch."Applies-to Doc. Line No.") then begin
                    if SalesShptLine."Item Shpt. Entry No." <> 0 then
                        exit(CostCalcMgt.SplitItemLedgerEntriesExist(TempItemLedgEntry, -SalesShptLine."Quantity (Base)", SalesShptLine."Item Shpt. Entry No."));
                    exit(true);
                end;
            TempItemChargeAssgntPurch."Applies-to Doc. Type"::"Return Receipt":
                if ReturnRcptLine.Get(TempItemChargeAssgntPurch."Applies-to Doc. No.", TempItemChargeAssgntPurch."Applies-to Doc. Line No.") then begin
                    if ReturnRcptLine."Item Rcpt. Entry No." <> 0 then
                        exit(CostCalcMgt.SplitItemLedgerEntriesExist(TempItemLedgEntry, ReturnRcptLine."Quantity (Base)", ReturnRcptLine."Item Rcpt. Entry No."));
                    exit(true);
                end;
            TempItemChargeAssgntPurch."Applies-to Doc. Type"::"Transfer Receipt":
                if TransRcptLine.Get(TempItemChargeAssgntPurch."Applies-to Doc. No.", TempItemChargeAssgntPurch."Applies-to Doc. Line No.") then begin
                    if TransRcptLine."Item Rcpt. Entry No." <> 0 then
                        exit(CostCalcMgt.SplitItemLedgerEntriesExist(TempItemLedgEntry, TransRcptLine."Quantity (Base)", TransRcptLine."Item Rcpt. Entry No."));
                    exit(true);
                end;
        end;

        exit(false);
    end;

    local procedure CheckAndUpdateSustainabilityInvoicePostingBuffer(var InvoicePostingBuffer: Record "Invoice Posting Buffer" temporary; var PurchaseLine: Record "Purchase Line")
    var
        PurchaseHeader: Record "Purchase Header";
        AccountCategory: Record "Sustain. Account Category";
        SustainabilityPostMgt: Codeunit "Sustainability Post Mgt";
        GHGCredit: Boolean;
        Sign: Integer;
        CO2ToPost: Decimal;
        CH4ToPost: Decimal;
        N2OToPost: Decimal;
        CO2eEmission: Decimal;
        CarbonFee: Decimal;
        Denominator: Decimal;
    begin
        PurchaseHeader := PurchaseLine.GetPurchHeader();
        GHGCredit := IsGHGCreditLine(PurchaseLine);

        if GHGCredit then begin
            PurchaseLine.TestField("Emission CH4 Per Unit", 0);
            PurchaseLine.TestField("Emission N2O Per Unit", 0);
        end;

        Sign := GetPostingSign(PurchaseHeader, GHGCredit);

        CO2ToPost := PurchaseLine."Emission CO2 Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";
        CH4ToPost := PurchaseLine."Emission CH4 Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";
        N2OToPost := PurchaseLine."Emission N2O Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";

        CO2ToPost := CO2ToPost * Sign;
        CH4ToPost := CH4ToPost * Sign;
        N2OToPost := N2OToPost * Sign;

        if not SustainabilitySetup.IsValueChainTrackingEnabled() then
            exit;

        if not CanPostSustainabilityJnlLine(PurchaseLine, CO2ToPost, CH4ToPost, N2OToPost, 0, false) then
            exit;

        InvoicePostingBuffer."Sust. Account No." := PurchaseLine."Sust. Account No.";
        InvoicePostingBuffer."Sust. Account Name" := PurchaseLine."Sust. Account Name";
        InvoicePostingBuffer."Sust. Account Category" := PurchaseLine."Sust. Account Category";
        InvoicePostingBuffer."Sust. Account Subcategory" := PurchaseLine."Sust. Account Subcategory";

        AccountCategory.Get(PurchaseLine."Sust. Account Category");
        SustainabilityPostMgt.UpdateCarbonFeeEmissionValues(AccountCategory."Emission Scope", PurchaseHeader."Posting Date", PurchaseHeader."Buy-from Country/Region Code", CO2ToPost, N2OToPost, CH4ToPost, CO2eEmission, CarbonFee);

        Denominator := PurchaseLine."Qty. per Unit of Measure" * PurchaseLine."Qty. to Invoice";
        if Denominator = 0 then
            exit;

        InvoicePostingBuffer."CO2e per Unit" := CO2eEmission / Denominator;
        InvoicePostingBuffer."Total CO2e" := PurchaseLine."Qty. to Invoice" * InvoicePostingBuffer."CO2e per Unit";
    end;

    local procedure UpdatePostedSustainabilityEmissionOrderLine(PurchHeader: Record "Purchase Header"; var TempPurchLine: Record "Purchase Line" temporary)
    var
        PostedEmissionCO2: Decimal;
        PostedEmissionCH4: Decimal;
        PostedEmissionN2O: Decimal;
        PostedEnergyConsumption: Decimal;
        PostedEmissionCost: Decimal;
        GHGCredit: Boolean;
        Sign: Integer;
    begin
        GHGCredit := IsGHGCreditLine(TempPurchLine);
        Sign := GetPostingSign(PurchHeader, GHGCredit);

        UpdatePostedSustainabilityEmission(TempPurchLine, TempPurchLine."Qty. to Invoice", Sign, PostedEmissionCO2, PostedEmissionCH4, PostedEmissionN2O, PostedEnergyConsumption, PostedEmissionCost);
        TempPurchLine."Posted Emission CO2" += PostedEmissionCO2;
        TempPurchLine."Posted Emission CH4" += PostedEmissionCH4;
        TempPurchLine."Posted Emission N2O" += PostedEmissionN2O;
        TempPurchLine."Posted Energy Consumption" += PostedEnergyConsumption;
        TempPurchLine."Posted Total Emission Cost" += PostedEmissionCost;
    end;

    local procedure UpdatePostedSustainabilityEmission(PurchaseLine: Record "Purchase Line"; Quantity: Decimal; Sign: Integer; var PostedEmissionCO2: Decimal; var PostedEmissionCH4: Decimal; var PostedEmissionN2O: Decimal; var PostedEnergyConsumption: Decimal; var PostedEmissionCost: Decimal)
    begin
        PostedEmissionCO2 := (PurchaseLine."Emission CO2 Per Unit" * Abs(Quantity) * PurchaseLine."Qty. per Unit of Measure") * Sign;
        PostedEmissionCH4 := (PurchaseLine."Emission CH4 Per Unit" * Abs(Quantity) * PurchaseLine."Qty. per Unit of Measure") * Sign;
        PostedEmissionN2O := (PurchaseLine."Emission N2O Per Unit" * Abs(Quantity) * PurchaseLine."Qty. per Unit of Measure") * Sign;
        PostedEnergyConsumption := (PurchaseLine."Energy Consumption Per Unit" * Abs(Quantity) * PurchaseLine."Qty. per Unit of Measure") * Sign;
        PostedEmissionCost := (PurchaseLine."Emission Cost Per Unit" * Abs(Quantity) * PurchaseLine."Qty. per Unit of Measure") * Sign;
    end;

    local procedure PostSustainabilityLine(PurchaseHeader: Record "Purchase Header"; var PurchaseLine: Record "Purchase Line"; SrcCode: Code[10]; GenJnlLineDocNo: Code[20])
    var
        SustainabilityJnlLine: Record "Sustainability Jnl. Line";
        SustainabilityPostMgt: Codeunit "Sustainability Post Mgt";
        GHGCredit: Boolean;
        Sign: Integer;
        CO2ToPost: Decimal;
        CH4ToPost: Decimal;
        N2OToPost: Decimal;
        EnergyConsumptionToPost: Decimal;
    begin
        GHGCredit := IsGHGCreditLine(PurchaseLine);

        if GHGCredit then begin
            PurchaseLine.TestField("Emission CH4 Per Unit", 0);
            PurchaseLine.TestField("Emission N2O Per Unit", 0);
        end;

        Sign := GetPostingSign(PurchaseHeader, GHGCredit);

        CO2ToPost := PurchaseLine."Emission CO2 Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";
        CH4ToPost := PurchaseLine."Emission CH4 Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";
        N2OToPost := PurchaseLine."Emission N2O Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";
        EnergyConsumptionToPost := PurchaseLine."Energy Consumption Per Unit" * Abs(PurchaseLine."Qty. to Invoice") * PurchaseLine."Qty. per Unit of Measure";

        CO2ToPost := CO2ToPost * Sign;
        CH4ToPost := CH4ToPost * Sign;
        N2OToPost := N2OToPost * Sign;
        EnergyConsumptionToPost := EnergyConsumptionToPost * Sign;

        if not CanPostSustainabilityJnlLine(PurchaseLine, CO2ToPost, CH4ToPost, N2OToPost, EnergyConsumptionToPost, true) then
            exit;

        SustainabilityJnlLine.Init();
        SustainabilityJnlLine."Journal Template Name" := PurchaseHeader."Journal Templ. Name";
        SustainabilityJnlLine."Journal Batch Name" := '';
        SustainabilityJnlLine."Source Code" := SrcCode;
        SustainabilityJnlLine.Validate("Posting Date", PurchaseHeader."Posting Date");

        if GHGCredit then
            SustainabilityJnlLine.Validate("Document Type", SustainabilityJnlLine."Document Type"::"GHG Credit")
        else
            if PurchaseHeader."Document Type" in [PurchaseHeader."Document Type"::"Credit Memo", PurchaseHeader."Document Type"::"Return Order"] then
                SustainabilityJnlLine.Validate("Document Type", SustainabilityJnlLine."Document Type"::"Credit Memo")
            else
                SustainabilityJnlLine.Validate("Document Type", SustainabilityJnlLine."Document Type"::Invoice);

        SustainabilityJnlLine.Validate("Document No.", GenJnlLineDocNo);
        SustainabilityJnlLine.Validate("Account No.", PurchaseLine."Sust. Account No.");
        SustainabilityJnlLine.Validate("Responsibility Center", PurchaseHeader."Responsibility Center");
        SustainabilityJnlLine.Validate("Reason Code", PurchaseHeader."Reason Code");
        SustainabilityJnlLine.Validate("Account Category", PurchaseLine."Sust. Account Category");
        SustainabilityJnlLine.Validate("Account Subcategory", PurchaseLine."Sust. Account Subcategory");
        if PurchaseLine."Unit for Sust. Formulas" <> '' then
            SustainabilityJnlLine.Validate("Unit of Measure", PurchaseLine."Unit for Sust. Formulas")
        else
            SustainabilityJnlLine.Validate("Unit of Measure", PurchaseLine."Unit of Measure Code");

        SustainabilityJnlLine."Fuel/Electricity" := PurchaseLine."Fuel/Electricity";
        SustainabilityJnlLine.Distance := PurchaseLine.Distance;
        SustainabilityJnlLine."Installation Multiplier" := PurchaseLine."Installation Multiplier";
        SustainabilityJnlLine."Custom Amount" := PurchaseLine."Custom Amount";
        SustainabilityJnlLine."Time Factor" := PurchaseLine."Time Factor";
        SustainabilityJnlLine.Validate("Energy Source Code", PurchaseLine."Energy Source Code");
        SustainabilityJnlLine."Dimension Set ID" := PurchaseLine."Dimension Set ID";
        SustainabilityJnlLine."Shortcut Dimension 1 Code" := PurchaseLine."Shortcut Dimension 1 Code";
        SustainabilityJnlLine."Shortcut Dimension 2 Code" := PurchaseLine."Shortcut Dimension 2 Code";
        SustainabilityJnlLine.Validate("Emission CO2", CO2ToPost);
        SustainabilityJnlLine.Validate("Emission CH4", CH4ToPost);
        SustainabilityJnlLine.Validate("Emission N2O", N2OToPost);
        SustainabilityJnlLine.Validate("Energy Consumption", EnergyConsumptionToPost);
        SustainabilityJnlLine.Validate("Country/Region Code", PurchaseHeader."Buy-from Country/Region Code");
        SustainabilityJnlLine.Validate("Renewable Energy", PurchaseLine."Renewable Energy");
        OnPostSustainabilityLineOnBeforeInsertLedgerEntry(SustainabilityJnlLine, PurchaseHeader, PurchaseLine);
        SustainabilityPostMgt.InsertLedgerEntry(SustainabilityJnlLine);

        UpdateDefaultEmissionOnMaster(PurchaseLine);
    end;

    local procedure GetPostingSign(PurchaseHeader: Record "Purchase Header"; GHGCredit: Boolean): Integer
    var
        Sign: Integer;
    begin
        Sign := 1;

        case PurchaseHeader."Document Type" of
            PurchaseHeader."Document Type"::"Credit Memo", PurchaseHeader."Document Type"::"Return Order":
                if not GHGCredit then
                    Sign := -1;
            else
                if GHGCredit then
                    Sign := -1;
        end;

        exit(Sign);
    end;

    local procedure IsGHGCreditLine(PurchaseLine: Record "Purchase Line"): Boolean
    var
        Item: Record Item;
    begin
        if PurchaseLine.Type <> PurchaseLine.Type::Item then
            exit(false);

        if PurchaseLine."No." = '' then
            exit(false);

        Item.Get(PurchaseLine."No.");

        exit(Item."GHG Credit");
    end;

    local procedure IsGHGCreditLine(PurchRcptLine: Record "Purch. Rcpt. Line"): Boolean
    var
        Item: Record Item;
    begin
        if PurchRcptLine.Type <> PurchRcptLine.Type::Item then
            exit(false);

        if PurchRcptLine."No." = '' then
            exit(false);

        Item.Get(PurchRcptLine."No.");

        exit(Item."GHG Credit");
    end;

    local procedure UpdateSustainabilityItemJournalLine(var ItemJournalLine: Record "Item Journal Line"; var PurchRcptLine: Record "Purch. Rcpt. Line")
    var
        GHGCredit: Boolean;
        Sign: Integer;
        QtyToPost: Decimal;
        CO2ToPost: Decimal;
        CH4ToPost: Decimal;
        N2OToPost: Decimal;
    begin
        if not SustainabilitySetup.IsValueChainTrackingEnabled() then
            exit;

        GHGCredit := IsGHGCreditLine(PurchRcptLine);

        Sign := -1;
        if GHGCredit then
            Sign := 1;

        if ItemJournalLine."Invoiced Qty. (Base)" <> 0 then
            QtyToPost := Abs(ItemJournalLine."Invoiced Qty. (Base)")
        else
            QtyToPost := Abs(ItemJournalLine."Quantity (Base)");

        CO2ToPost := PurchRcptLine."Emission CO2 Per Unit" * QtyToPost * PurchRcptLine."Qty. per Unit of Measure" * Sign;
        CH4ToPost := PurchRcptLine."Emission CH4 Per Unit" * QtyToPost * PurchRcptLine."Qty. per Unit of Measure" * Sign;
        N2OToPost := PurchRcptLine."Emission N2O Per Unit" * QtyToPost * PurchRcptLine."Qty. per Unit of Measure" * Sign;

        if (CO2ToPost = 0) and (CH4ToPost = 0) and (N2OToPost = 0) then
            exit;

        ItemJournalLine."Sust. Account No." := PurchRcptLine."Sust. Account No.";
        ItemJournalLine."Sust. Account Name" := PurchRcptLine."Sust. Account Name";
        ItemJournalLine."Sust. Account Category" := PurchRcptLine."Sust. Account Category";
        ItemJournalLine."Sust. Account Subcategory" := PurchRcptLine."Sust. Account Subcategory";
        ItemJournalLine."Emission CO2" := CO2ToPost;
        ItemJournalLine."Emission CH4" := CH4ToPost;
        ItemJournalLine."Emission N2O" := N2OToPost;
    end;

    local procedure CanPostSustainabilityJnlLine(PurchaseLine: Record "Purchase Line"; CO2ToPost: Decimal; CH4ToPost: Decimal; N2OToPost: Decimal; EnergyConsumptionToPost: Decimal; CalledFromLedger: Boolean): Boolean
    var
        SustAccountCategory: Record "Sustain. Account Category";
        SustainAccountSubcategory: Record "Sustain. Account Subcategory";
    begin
        if PurchaseLine."Sust. Account No." = '' then
            exit(false);

        if SustAccountCategory.Get(PurchaseLine."Sust. Account Category") then
            if SustAccountCategory."Water Intensity" or SustAccountCategory."Waste Intensity" or SustAccountCategory."Discharged Into Water" then
                Error(NotAllowedToPostSustLedEntryForWaterOrWasteErr, PurchaseLine."Sust. Account No.");

        if CalledFromLedger then
            if SustainAccountSubcategory.Get(PurchaseLine."Sust. Account Category", PurchaseLine."Sust. Account Subcategory") then
                if SustainAccountSubcategory."Energy Value Required" then
                    PurchaseLine.TestField("Energy Consumption");

        if not PurchaseLine."Renewable Energy" then
            if (CO2ToPost = 0) and (CH4ToPost = 0) and (N2OToPost = 0) and (EnergyConsumptionToPost = 0) then
                Error(EmissionMustNotBeZeroErr);

        if (PurchaseLine.Type = PurchaseLine.Type::"Fixed Asset") and (PurchaseLine."FA Posting Type" <> PurchaseLine."FA Posting Type"::"Acquisition Cost") then
            Error(AllowedToPostSustainabilityEntryForAcquisitionErr);

        if (CO2ToPost <> 0) or (CH4ToPost <> 0) or (N2OToPost <> 0) or (EnergyConsumptionToPost <> 0) then
            exit(true);
    end;

    local procedure UpdateDefaultEmissionOnMaster(PurchaseLine: Record "Purchase Line")
    var
        Item: Record Item;
        Resource: Record Resource;
        ItemCharge: Record "Item Charge";
    begin
        case PurchaseLine.Type of
            PurchaseLine.Type::Item:
                begin
                    Item.Get(PurchaseLine."No.");
                    if (Item."Default Sust. Account" = '') or (Item."Replenishment System" <> Item."Replenishment System"::Purchase) then
                        exit;

                    Item.Validate("Default CO2 Emission", PurchaseLine."Emission CO2 Per Unit");
                    if not Item."GHG Credit" then begin
                        Item.Validate("Default CH4 Emission", PurchaseLine."Emission CH4 Per Unit");
                        Item.Validate("Default N2O Emission", PurchaseLine."Emission N2O Per Unit");
                    end;

                    Item.Modify();
                end;
            PurchaseLine.Type::Resource:
                begin
                    Resource.Get(PurchaseLine."No.");
                    if (Resource."Default Sust. Account" = '') then
                        exit;

                    Resource.Validate("Default CO2 Emission", PurchaseLine."Emission CO2 Per Unit");
                    Resource.Validate("Default CH4 Emission", PurchaseLine."Emission CH4 Per Unit");
                    Resource.Validate("Default N2O Emission", PurchaseLine."Emission N2O Per Unit");
                    Resource.Modify();
                end;
            PurchaseLine.Type::"Charge (Item)":
                begin
                    ItemCharge.Get(PurchaseLine."No.");
                    if (ItemCharge."Default Sust. Account" = '') then
                        exit;

                    ItemCharge.Validate("Default CO2 Emission", PurchaseLine."Emission CO2 Per Unit");
                    ItemCharge.Validate("Default CH4 Emission", PurchaseLine."Emission CH4 Per Unit");
                    ItemCharge.Validate("Default N2O Emission", PurchaseLine."Emission N2O Per Unit");
                    ItemCharge.Modify();
                end;
        end
    end;

    local procedure TryBindPostingPreviewHandler(): Boolean
    var
        SustPreviewPostingHandler: Codeunit "Sust. Preview Posting Handler";
        SustPreviewPostInstance: Codeunit "Sust. Preview Post Instance";
    begin
        SustPreviewPostInstance.Initialize();
        exit(SustPreviewPostingHandler.TryBindPostingPreviewHandler());
    end;

    local procedure TryUnbindPostingPreviewHandler(): Boolean
    var
        SustPreviewPostingHandler: Codeunit "Sust. Preview Posting Handler";
    begin
        exit(SustPreviewPostingHandler.TryUnbindPostingPreviewHandler());
    end;

    var
        SustainabilitySetup: Record "Sustainability Setup";
        EmissionMustNotBeZeroErr: Label 'The Emission fields must have a value that is not 0.';
        NotAllowedToPostSustLedEntryForWaterOrWasteErr: Label 'It is not allowed to post Sustainability Ledger Entry for water or waste in purchase document for Account No. %1', Comment = '%1 = Sustainability Account No.';
        AllowedToPostSustainabilityEntryForAcquisitionErr: Label 'It is only allowed to post Sustainability Entry for Acquisition Cost.';

    [IntegrationEvent(false, false)]
    local procedure OnPostSustainabilityLineOnBeforeInsertLedgerEntry(var SustainabilityJnlLine: Record "Sustainability Jnl. Line"; PurchaseHeader: Record "Purchase Header"; PurchaseLine: Record "Purchase Line")
    begin
    end;
}